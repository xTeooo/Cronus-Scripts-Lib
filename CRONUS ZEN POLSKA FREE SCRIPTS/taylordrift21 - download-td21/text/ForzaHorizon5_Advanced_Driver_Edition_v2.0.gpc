/*
							__________                                    ___ ___                   __                            _______
							\_   _____/  ____  _______ _____________     /   |   \   ____  _______ |__|________  ____    ____    |   ____/
							 |    __)   /  _ \ \_  __ \\___   /\__  \   /    ~    \ /  _ \ \_  __ \|  |\___   / /  _ \  /    \   |____  \ 
							 |     \   (  <_> ) |  | \/ /    /  / __ \_ \    Y    /(  <_> ) |  | \/|  | /    / (  <_> )|   |  \  /       \
							 \___  /    \____/  |__|   /_____ \(____  /  \___|_  /  \____/  |__|   |__|/_____ \ \____/ |___|  / /______  /
							     \/                          \/     \/         \/                            \/             \/         \/ 

   _____        __                                             ___ ________            __                         ___________     ___ __   __    __                 
  /  _  \    __| _/___  _______     ____    ____    ____    __| _/ \______ \  _______ |__|___  __  ____  _______  \_   _____/  __| _/|__|_/  |_ |__|  ____    ____  
 /  /_\  \  / __ | \  \/ /\__  \   /    \ _/ ___\ _/ __ \  / __ |   |    |  \ \_  __ \|  |\  \/ /_/ __ \ \_  __ \  |    __)_  / __ | |  |\   __\|  | /  _ \  /    \ 
/    |    \/ /_/ |  \   /  / __ \_|   |  \\  \___ \  ___/ / /_/ |   |    `   \ |  | \/|  | \   / \  ___/  |  | \/  |        \/ /_/ | |  | |  |  |  |(  <_> )|   |  \
\____|__  /\____ |   \_/  (____  /|___|  / \___  > \___  >\____ |  /_______  / |__|   |__|  \_/   \___  > |__|    /_______  /\____ | |__| |__|  |__| \____/ |___|  /
        \/      \/             \/      \/      \/      \/      \/          \/                         \/                  \/      \/                             \/ 
							     								

Script Author        = Taylordrift21 
Additional Credits   = Batts - Swizzy - Cronus Community  
Script Version 	     = v2.0 
Script Compatibility = 16bit & 32bit 

*/

int assistedBrakes;
int tractionControl; 
int autoClutch;
int autoShifting;
int steeringAssist; 
int launchControl; 

const string toggle     []     = {"ON","OFF",""};
const string titleScreen[]     = {"Forza Horizon 5","Dashboard","In Game","Menu","ABS","TC","AC","AS","SA","LC","OffRoad Detected",""};
const string modNames   []     = {"Assisted Brakes","Traction Control","Auto Clutch","Auto Shift","Steering Assist","Lauch Control",""}; 

int modIdx,modNameIdx,inGameMenu;
int modMenu;
int displayTitle = TRUE;
int blankScreen,screenSaver,i;
int updateDisplay;
int brakingInitiated;
int brakingTimeInitiated = 600; 
int gearIdx; 
int offRoadActive; 
int gearChangeTimer;
int takeOffTime = 800; 
int activateFirstGear;
int activateOtherGears; 
int afterTakeOff = 1500; 
int steeringCorrection;
int AccelerationValue;
int BrakePressure;
int GearIdx,GearUp,GearDown,Clutch,Accelerator,BrakePedal,HandBrake,ActivateThrottle,AccelerationRate;
const int8 Throttle[] = {
0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,
30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,
57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,76,77,78,79,80,81,82,83,84,85,86,87,88,89,90,91,92,93,94,95,96,97,98,99,100,100};
const int8 ABS[] = {
0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,
30,31,32,33,34,35,36,37,38,39,40,41,42,43,44,45,46,47,48,49,50,51,52,53,54,55,56,
57,58,59,60,61,62,63,64,65,66,67,68,69,70,71,72,73,74,75,75};
const int16 TIME [] = { 100 , 100 , 1200};
const int8  DrivingControls[] = { PS4_R2, PS4_L2,PS4_CIRCLE , PS4_SQUARE , PS4_L1 , PS4_CROSS}; 
const uint8 GearsMinMax [][] = { 
{ 0 , 7 }}; 
init { 
    Accelerator = DrivingControls[0];
    BrakePedal  = DrivingControls[1];
	GearUp      = DrivingControls[2];
	GearDown    = DrivingControls[3];
	Clutch      = DrivingControls[4];
	HandBrake   = DrivingControls[5];
} 
main { 
	if(get_ival(PS4_L2)){ 
		if(event_press(PS4_OPTIONS)){ 
			getMenuStatus(TRUE,TRUE,FALSE)
			modNameIdx = 0;
			combo_run(rumbleOn);
	
			if(!modMenu){ 
				getMenuStatus(FALSE,FALSE,TRUE)
			} 
		}
		set_val(PS4_OPTIONS,0);
	} 
    if(!displayTitle){ 
    	set_rgb(0,0,255); 
		if(modMenu){ 	
			modNameIdx = menuNavigation(modNameIdx,0,5);
			modIdx = -1;
			assistedBrakes   = toggleSwitch(assistedBrakes);
			tractionControl  = toggleSwitch(tractionControl);
			autoClutch       = toggleSwitch(autoClutch);
			autoShifting     = toggleSwitch(autoShifting);
			steeringAssist   = toggleSwitch(steeringAssist);
			launchControl    = toggleSwitch(launchControl); 
		}
	}
	if(updateDisplay){
		
		cls_oled(0);
		rect_oled(0, 0, OLED_WIDTH, OLED_HEIGHT, OLED_BLACK, OLED_WHITE); 
		line_oled(0,28,127 , 28, 1, 1);

        if(modMenu) { 
        	print(centerPosition(getStringLength(modNames[modNameIdx]) ,OLED_FONT_SMALL_WIDTH), 10  ,OLED_FONT_SMALL , OLED_WHITE , modNames[modNameIdx]);
        	modIdx = -1; 
        	displayToggle(assistedBrakes);
        	displayToggle(tractionControl);
        	displayToggle(autoClutch);
        	displayToggle(autoShifting);
        	displayToggle(steeringAssist);
        	displayToggle(launchControl); 
        }
		updateDisplay = FALSE;
	}
	if(!modMenu){ 
		if(displayTitle){ 
			cls_oled(OLED_BLACK);
	   	    rect_oled(0, 0, OLED_WIDTH, OLED_HEIGHT, OLED_BLACK, OLED_WHITE);
	   	    print(centerPosition(getStringLength(titleScreen[0]),OLED_FONT_SMALL_WIDTH),5,OLED_FONT_SMALL,OLED_WHITE,titleScreen[0]);
	   	    print(centerPosition(getStringLength(titleScreen[1]),OLED_FONT_SMALL_WIDTH),20,OLED_FONT_SMALL,OLED_WHITE,titleScreen[1]);
	   	    if(!offRoadActive){ 
		   	    if(assistedBrakes){
		   	    	print(10,37,0,1,titleScreen[4]);
		   	    }
		   	    if(tractionControl){
		   	    	print(10,50,0,1,titleScreen[5]);
		   	    }
		   	    if(autoClutch){ 
		   	    	print(105,37,0,1,titleScreen[6]);
		   	    }
		   	    if(autoShifting){ 
		   	    	print(105,50,0,1,titleScreen[7]);
		   	    } 
		   	    if(steeringAssist){ 
		   	    	print(centerPosition(getStringLength(titleScreen[8]),OLED_FONT_SMALL_WIDTH),50,OLED_FONT_SMALL,OLED_WHITE,titleScreen[8]);
		   	    } 
		   	    if(launchControl){ 
		   	    	print(centerPosition(getStringLength(titleScreen[9]),OLED_FONT_SMALL_WIDTH),37,OLED_FONT_SMALL,OLED_WHITE,titleScreen[9]);
		   	    }  
		   	}
	   	    if(offRoadActive) { 
	   	    	screenSaver = FALSE;
	   	    	cls_oled(OLED_BLACK);
	   	    	rect_oled(0, 0, OLED_WIDTH, OLED_HEIGHT, OLED_BLACK, OLED_WHITE);
	   	    	print(centerPosition(getStringLength(titleScreen[0]),OLED_FONT_SMALL_WIDTH),5,OLED_FONT_SMALL,OLED_WHITE,titleScreen[0]);
	   	    	print(centerPosition(getStringLength(titleScreen[1]),OLED_FONT_SMALL_WIDTH),20,OLED_FONT_SMALL,OLED_WHITE,titleScreen[1]);
	   	    	print(centerPosition(getStringLength(titleScreen[10]),OLED_FONT_SMALL_WIDTH),42,OLED_FONT_SMALL,OLED_WHITE,titleScreen[10]);
	   	    } 
	   	    	
	   	    	
	   	    displayTitle = FALSE;
	   	    screenSaver  = TRUE;
	   	}   
	    if(screenSaver){  
    		blankScreen += get_rtime();
    	
			if(blankScreen >= 10000)
			{
				cls_oled(OLED_BLACK);
				blankScreen = 0;
				screenSaver = FALSE;
	    	} 
	    }
	    if(get_val(PS4_OPTIONS) && get_ptime(PS4_OPTIONS) > 300){ 
	    	inGameMenu = TRUE;
	    	set_val(PS4_OPTIONS,0);
	    } 
	    if(inGameMenu){ 
	    	combo_run(gameMenuMsg);
	    	set_ledx(1,1);
	    
			if(event_press(PS4_OPTIONS)){
				inGameMenu = FALSE;
				combo_run(gameMenuMsgExit); 
				combo_run(rumbleOff); 
    		} 
    		set_val(PS4_OPTIONS,0);
	    } 
	    if(!inGameMenu){ 
		    if(assistedBrakes) { 
		    	BrakePressure = ABS[clamp(BrakePressure ++,0,sizeof(ABS))];
				if(get_rumble(RUMBLE_LT)){
					set_val(BrakePedal,BrakePressure);
				} 
				if(event_release(BrakePedal) || get_ival(Accelerator)){
					BrakePressure = 0;
				} 
			} 
			if(tractionControl){ 
				if(get_rumble(RUMBLE_RT) >= 0){ 
					if(get_ival(Accelerator) > AccelerationValue){ 
						AccelerationValue = Throttle[clamp(AccelerationValue ++,0,sizeof(Throttle))]; 
					}else{ 
						AccelerationValue = get_ival(Accelerator);		
					}
					set_val(Accelerator,AccelerationValue);
				} 
				if(event_release(Accelerator)){ 
					AccelerationValue = 0;
				} 
			}
			if(autoClutch){ 
				if(event_press(GearUp)){ 
					GearIdx = clamp(GearIdx + 1,GearsMinMax[0][0],GearsMinMax[0][1]);
					combo_run(Clutch);
				}
				if(event_press(GearDown)){ 
					GearIdx = clamp(GearIdx - 1,GearsMinMax[0][0],GearsMinMax[0][1]);
					combo_run(Clutch);
				}
				if(event_press(GearDown) && !get_rumble(RUMBLE_A) && !get_rumble(RUMBLE_B)){
					GearIdx = 1;
				}
				if(get_ival(GearDown) && get_ptime(GearDown) > 300){ 
					GearIdx = 1;
				} 
			}
			if(launchControl){ 
				if(event_press(PS4_R3)){
					combo_run(Launch);
					GearIdx = 1;
				} 
				if(ActivateThrottle){ 
				
					if(event_press(PS4_UP)){
						AccelerationRate = clamp(AccelerationRate + 1,35,60);
					} 
					if(event_press(PS4_DOWN)){
						AccelerationRate = clamp(AccelerationRate - 1,35,60);
					}
					set_val(Accelerator,AccelerationRate/2);
					set_val(HandBrake,100);
					set_val(Clutch,100);
					set_val(PS4_DOWN,0);
					set_val(PS4_UP,0);
				} 
				if(get_ival(Accelerator)){
					ActivateThrottle = FALSE;
					combo_run(Delay); 
				}
			}
			if(autoShifting){ 
				if((get_rumble(RUMBLE_A) > 0 && get_rumble(RUMBLE_A) <= 100) && get_rumble(RUMBLE_A) != 10){ 
					if((get_rumble(RUMBLE_B) > 0 && get_rumble(RUMBLE_B) <= 100) && get_rumble(RUMBLE_B) != 10){ 
						offRoadActive = TRUE;
						displayTitle = TRUE;
					}
				}
				else { 
					offRoadActive = FALSE;
				}
				if(get_rumble(RUMBLE_A) > 15){ 
					if((get_rumble(RUMBLE_B) > 0 && get_rumble(RUMBLE_B) <= 8)){ 
						if(!get_val(Accelerator) && !get_rumble(RUMBLE_RT) && !get_rumble(RUMBLE_LT)){ 
							if(gearIdx != 1)
								combo_run(gearDown);	
							else
								combo_stop(gearDown);
			
								combo_run(Clutch);
						} 
					}
				} 
				if(get_ival(Accelerator)){ 
					gearChangeTimer = get_ptime(Accelerator);
					if(get_rumble(RUMBLE_A) != 5 && get_rumble(RUMBLE_B) != 10){ 
						if(gearChangeTimer <= takeOffTime)  { 
							activateFirstGear = TRUE; 
						} 
						if((gearChangeTimer > takeOffTime) && (gearChangeTimer <= afterTakeOff)){ 
							activateOtherGears = TRUE;
							activateFirstGear = FALSE; 	
						} 
					} 
				} 
				if(event_release(Accelerator)){ 
					gearChangeTimer = 0;
					activateOtherGears = FALSE;
				} 
				if(!offRoadActive){ 
					if(get_val(Accelerator)){ 
						if(activateFirstGear){ 
							if(get_rumble(RUMBLE_A) <= 5){ 
								if(get_rumble(RUMBLE_B) == 10){ 
									if(gearIdx != 7)
										combo_run(gearUp);	
									else
										combo_stop(gearUp);
										
									combo_run(Clutch);
									
								} 
							}
						}
						else if(activateOtherGears){ 
							if(get_rumble(RUMBLE_A) <= 5){ 
								if(get_rumble(RUMBLE_B) == 10){ 
									if(gearIdx != 7)
										combo_run(gearUp);	
									else
										combo_stop(gearUp);
										
									combo_run(Clutch);
								}
							}
						}
					} 
				} 
				if(get_val(BrakePedal)){ 
					brakingInitiated = get_ptime(BrakePedal); 
					if(brakingInitiated > brakingTimeInitiated){  // Hard Braking \\ 
						if(get_rumble(RUMBLE_LT) >= 12){ 
							if(gearIdx != 1)
								combo_run(gearDown);	
							else
								combo_stop(gearDown);
								combo_run(Clutch);
								activateOtherGears = FALSE; 
						}
					} 
					else if(brakingInitiated >= 150 && brakingInitiated <= 150){  // Easy Braking \\ 
						if(gearIdx != 1)
							combo_run(gearDown);	
						else
							combo_stop(gearDown);
							combo_run(Clutch);
					}
				} 
				if(event_release(BrakePedal)){ 
					brakingInitiated = 0; 
				} 
				if(get_val(HandBrake) && get_ptime(HandBrake) > 150){ 
					if(gearIdx != 1)
						combo_run(gearDown);
					else
						combo_stop(gearDown);
						
					combo_run(Clutch);
				} 
			}
			if(steeringAssist){ 
				if(get_val(XB1_LX) <= -100 || get_val(XB1_LX) >= 100){ 
					steeringCorrection = TRUE;
				} 
				if(steeringCorrection){ 
					if(get_val(XB1_LX) <= -100){ 
						combo_run(correctionLeft);
					}
					if(get_val(XB1_LX) >= 100){ 
						combo_run(correctionRight);
					}
				}
			} 
		} 
    }
}
combo gameMenuMsg { 
cls_oled(OLED_BLACK);
print(centerPosition(getStringLength(titleScreen[2]),OLED_FONT_MEDIUM_WIDTH),10,OLED_FONT_MEDIUM,OLED_WHITE,titleScreen[2])
print(centerPosition(getStringLength(titleScreen[3]),OLED_FONT_MEDIUM_WIDTH),35,OLED_FONT_MEDIUM,OLED_WHITE,titleScreen[3])
}
combo gameMenuMsgExit { 
cls_oled(OLED_BLACK);
wait(100);
displayTitle = TRUE;
} 
combo gearDown {
    set_val(XB1_X, 100);
    wait(100);
    set_val(XB1_X, 0);
    wait(100);
    set_val(XB1_X, 0);
    gearIdx  = clamp(gearIdx - 1,1,7);
}
combo gearUp {
    set_val(XB1_B, 100);
    wait(40);
    set_val(XB1_B, 0);
    wait(40);
    set_val(XB1_B, 0);
    gearIdx  = clamp(gearIdx + 1,1,7); 
}
function displayToggle(fToggle){
	if(modNameIdx == modIdx++){    
    	if(fToggle == 1) { 
			rect_oled(3,31,60,30, OLED_BLACK, OLED_WHITE); 
			rect_oled(8,36,50,20, OLED_WHITE, OLED_WHITE);
			print(23,38,1,0,toggle[0]);
			rect_oled(73,36,50,20, OLED_WHITE, OLED_BLACK);
			rect_oled(68,31,60,30, OLED_BLACK, OLED_WHITE);
			print(83,38,1,1,toggle[1]);
		}	
		else if(fToggle == 0){ 
			rect_oled(73,36,50,20, OLED_WHITE, OLED_WHITE);
			rect_oled(68,31,60,30, OLED_BLACK, OLED_WHITE);
			print(83,38,1,0,toggle[1]);
			rect_oled(3,31,60,30, OLED_BLACK, OLED_WHITE); 
			rect_oled(8,36,50,20, OLED_WHITE, OLED_BLACK);
			print(23,38,1,1,toggle[0]);
		}
    }
} 
function toggleSwitch(Var){ 
	if(modNameIdx == modIdx++){ 
		if(event_press(PS4_UP)){ 
			Var = Refresh(Var ++,0,1);
			updateDisplay = TRUE;
			if(!Var)
				combo_run(rumbleOff);
			else
		    	combo_run(rumbleOn); 
		} 
		if(event_press(PS4_DOWN)){ 
			Var = Refresh(Var --,0,1);
			updateDisplay = TRUE;
			if(!Var)
				combo_run(rumbleOff);
			else
		   	 	combo_run(rumbleOn); 
		} 
	} 
	return Var;
} 
function menuNavigation(Index,fMin,fMax) { 
	if(!get_ival(PS4_L2)){ 
    	if(event_press(PS4_RIGHT)){
           	Index = Refresh(Index ++,fMin,fMax);
           	updateDisplay = TRUE;
    	}
    	if(event_press(PS4_LEFT)){
        	Index = Refresh(Index --,fMin,fMax);
            updateDisplay = TRUE; 
    	}
    }
    if(event_press(PS4_CIRCLE)){ 
    	getMenuStatus(FALSE,FALSE,TRUE)	
    } 
	BlockButtonPress ();
	return Index; 
}
function getMenuStatus(fUpdate,fModMenu,fDisplayTitle){ 	
	updateDisplay    = fUpdate;
	modMenu          = fModMenu;
	displayTitle     = fDisplayTitle;
} 
function Refresh(Value,Min,Max){  
    if(Value > Max) 
    	return Min;	 	
    if(Value < Min)
    	return Max;     
    
    return Value;    
}
combo rumbleOn {
set_ledx(2,2);
set_rumble(RUMBLE_B, 50);
wait(150);
reset_rumble();
wait(100);
set_rumble(RUMBLE_B, 50);
wait(150);
reset_rumble();
reset_leds();
}
combo rumbleOff {						
set_ledx(1, 1);
set_rumble(RUMBLE_A, 50);
wait(300);
reset_rumble();
wait(400);
reset_leds();
}
combo Clutch {
set_val(Accelerator,0);
wait(TIME[0]);
set_val(Clutch,100) 
set_val(Accelerator,0);
wait(TIME[1]);
}
combo Launch { 
set_val(GearUp,100);
wait(TIME[1]); 
set_val(GearDown,100);
wait(TIME[1]);
tractionControl = FALSE;
ActivateThrottle = TRUE;
} 
combo Delay { 
wait(TIME[2]);
tractionControl = TRUE;
} 
/*
============================================================================================================================================
 Get String Length (Made By Swizzy) 
============================================================================================================================================
*/
int stringLength;
function getStringLength(offset) { 
    stringLength = 0;
    do { 
        offset++;
        stringLength++;
    } while (duint8(offset));
    return stringLength;
 } 
/*
============================================================================================================================================
  NumberToString () (Made By Batts)                                                                                                                     
============================================================================================================================================
*/   
int bufferIndex;
int charIndex,digitIndex;
function NumberToString(f_val,f_digits) {
    bufferIndex = 1;  
    digitIndex = 10000;
    if(f_val < 0) {                    //--neg numbers
         putc_oled(bufferIndex,45);    //--add leading "-"
         bufferIndex += 1;
         f_val = abs(f_val);
    } 
    for(charIndex = 5; charIndex >= 1; charIndex--) {
        if(f_digits >= charIndex) {
            putc_oled(bufferIndex,(f_val / digitIndex) + 48);
            f_val %= digitIndex;
            bufferIndex ++; 
            if(charIndex == 4) {
                putc_oled(bufferIndex,44);//--add ","
                bufferIndex ++;
            }
        }
        digitIndex /= 10;
    } 
    puts_oled(centerPosition(bufferIndex - 1,OLED_FONT_MEDIUM_WIDTH),30,OLED_FONT_MEDIUM,bufferIndex - 1,OLED_WHITE);
} 
int logVal;
function FindDigits(num) {
   logVal = 0;
   do {
      num /= 10;
      logVal++;
   } while (num);
   return logVal;
}
/*
============================================================================================================================================
 Center X Function (Made By Batts) 
============================================================================================================================================
*/
function centerPosition(f_chars,f_font) {

    return (OLED_WIDTH / 2) - ((f_chars * f_font) / 2);
} 
  /*
============================================================================================================================================
  Block Buttons()                                                                                                                      
============================================================================================================================================
*/
 const uint8 BlockButtons [] = {  
  PS4_L2,PS4_OPTIONS,PS4_LEFT, PS4_RIGHT,PS4_UP,PS4_DOWN,PS4_CROSS,PS4_CIRCLE,PS4_SQUARE,PS4_TRIANGLE,PS4_L1,PS4_R1,PS4_R2};
  
 function BlockButtonPress (){  
    for(i = 0; i < 13; i++){
    	if(get_ival(BlockButtons[i]) || event_press(BlockButtons[i])){
        	set_val(BlockButtons[i],0);
        }    
    }
}
combo correctionLeft { 
    offset(XB1_LX,75);
    wait(100);
    offset(XB1_LX,75);
    wait(100);
    offset(XB1_LX,-50);
    wait(100);
    offset(XB1_LX,-50);
    wait(100);
    offset(XB1_LX,25);
    wait(100);
    offset(XB1_LX,25);
    wait(100);
    offset(XB1_LX,-25);
    wait(100);
    offset(XB1_LX,-25);
    steeringCorrection = FALSE;
} 
combo correctionRight { 
    offset(XB1_LX,-75);
    wait(100);
    offset(XB1_LX,-75);
    wait(100);
    offset(XB1_LX,50);
    wait(100);
    offset(XB1_LX,50);
    wait(100);
    offset(XB1_LX,-25);
    wait(100);
    offset(XB1_LX,-25);
    wait(100);
    offset(XB1_LX,25);
    wait(100);
    offset(XB1_LX,25);
    steeringCorrection = FALSE;
} 

function offset(Stick,Value) {
  set_val(Stick, clamp(Value * (100 - abs(get_val(Stick))) / 100 + get_val(Stick), -100, 100));
  return;
}
 úÃ÷ÅõÍúÂôÃ÷ÆðÂó·