/*

Game: GTA V
Version 2.0
Device: CronusZEN ONLY
Controller tested: DS4 - Elite 2
Author: noozbar

	
	Hold L2 / LT + tap OPTIONS / Menu to open or close OLED Menu 
	
		while in Toggle Menu, 
			- tap TRIANGLE / Y to switch profile
			- tap UP or DOWN to toggle ON/OFF
			- tap LEFT or RIGHT to navigate
			- tap Cross / A for Edit values menu 
			
		while in Edit values menu
			- tap LEFT or RIGHT to navigate
			- hold CROSS / A and tap UP or RIGHT to increase value
			- hold CROSS / A and tap DOWN or LEFT to decrease value
			- tap CIRCLE / B to go back to Toggle Menu
			
	Tap PS / XBOX to save values and toggle and exit OLED Menu


Profile 1 Blue, 2 Purple and 3 Skyblue (12:00 o'clock - 1:30 o'clock - 3:00 o'clock)
Profile 4 Green (for all other wheel positions)


4 profiles with:
	Autorun
	
	Rumble Feedback
	
	C.E.O. Mode (turn ON if you are C.E.O. or OFF if you're not)
	
		- Fast Armor       > CIRCLE + LEFT
		- Fast Snack       > CIRCLE + RIGHT
		- Fast Sparrow     > CIRCLE + DOWN
		- Personal Vehicle > CIRCLE + UP
		
	Speed Toggle Autorun > L2 + UP
	
	Speed Toggle C.E.O.  > L2 + DOWN
	
	Easy Roll > L3
	
	Klaxon hold
	
	Vehicle Mode :
		tap TRIANGLE / Y or double tap DOWN (LED is Red, features are muted)

	
Additional features for profiles 1, 2 and 3:
	Antirecoil
	RapidFire
	BurstFire
	Sticky Assist
	AIM Abuse
	Headshot
	Speed Toggle AIM Abuse > L2 + RIGHT
	Speed Toggle HeadShot  > L2 + LEFT
	Change HeadShot Range  > L2 + double tap LEFT
	
*/

// Text to display 
	const string TITLE1 = "GTA"
	const string TITLE2 = "ONLINE"
	const string OFF = "OFF"; 
	const string ON = "ON"; 
	const string SAVE = "SAVED";
	const string ALL = "Every Profiles";
	const string MUTE = "Script OFF";
	const string HOLD = "Dbl-Tap CIRCLE/B";
	const string ACTIVE = "to turn ON"
	const string CLOSE = "Close"
	const string MID = "Mid"
	const string LONG = "Long"
	
	const string PROFILE[] = {
	"12:00 o'clock", "1:30 o'clock", "3:00 o'clock" };
	
	const string INFO_MENU_PS[] = {
	"Switch Profile:", "tap TRIANGLE", "Switch Mod:", "tap LEFT or RIGHT", "Toggle ON/OFF:", "tap UP or DOWN", 
	"Edit Menu:", "tap CROSS", "Close ModMenu:", "hold L2 +", "tap OPTION", "Save and exit:", "tap PS"}; 
	
	const string INFO_EDIT_PS[] = {
	"Switch Value:", "tap LEFT or RIGHT", "Edit Value:", "hold CROSS +", "UP DOWN LEFT RIGHT", 
	"Toggle Menu:", "tap CIRCLE", "Close ModMenu:", "hold L2 +", "tap OPTION", "Save and exit:", "tap PS"}; 
	
	const string INFO_MENU_XB[] = {
	"Switch Profile:", "tap Y", "Switch Mod:", "tap LEFT or RIGHT", "Toggle ON/OFF:", "tap UP or DOWN", 
	"Edit Menu:", "tap A", "Close ModMenu:", "hold LT +", "tap MENU", "Save and exit:", "tap XBOX"}; 
	
	const string INFO_EDIT_XB[] = {
	"Switch Value:", "tap LEFT or RIGHT", "Edit Value:", "hold A +", "UP DOWN LEFT RIGHT", 
	"Toggle Menu:", "tap B", "Close ModMenu:", "hold LT +", "tap MENU", "Save and exit:", "tap XBOX"}; 
	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

//                          M O D S   N A M E   A N D   T O G G L E S

// Mod Name
	const string MODNAME[] = {
			"AntiRecoil"    ,   "RapidFire"    ,    "BurstFire"    ,    "Sticky Assist"   ,  "AIM Abuse"  ,   "HeadShot"     ,  "AutoRun"    ,  "RumbleFeedBack"    ,  "C.E.O."   }; // add as many as you want
// modName_idx = 0          |        1         |      2            |       3              |        4        |      5         |      6        |      7               |      8
// _M mean MODNAME
	define 	ANTIRECOIL_M = 0,   RAPIDFIRE_M = 1,    BURSTFIRE_M = 2,    AIMASSIST_M = 3,     AIM_ABUSE_M = 4,  HEADSHOT_M = 5,  AUTORUN_M = 6,  RUMBLEFEEDBACK_M = 7, CEO_M = 8; // add as many as there are MODNAME[]
	
// Index to find Mod Name string - switchable in game with left/right in ModMenu 
	int modName_idx;

// Toggles for [3] Profiles: [profile_idx] = 0, 1 and 
	int toggle_antirecoil[3];  
	int toggle_rapidfire[3];  
	int toggle_burstfire[3];
	int toggle_aimassist[3];
	int toggle_aimabuse[3];
	int toggle_headshot[3];
	
// Amount of multi toggle - Used for display profiles
	define AMOUNT_OF_MULTI_TOGGLE = 6;

// Toggles
	int autorun_on;
	int rumble_on;
	int ceo_on;
	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

//                          V A L U E S   N A M E   A N D   V A R I A B L E S   F O R   V A L U E S

// Value Name
	const string VALNAME[] = {
			"AR Vertical" , "AR Horiz"   , "Rate of Fire" , 
// valName_idx = 0        |      1       |      2         |        
 			"BF Hold"    ,"BF Release", "Radius"  , "Steps", "Hold", "Release" };  // add as many as you want  
// valName_idx = 3       |      4     |      5    |    6   |    7  |     8   
	
// _V mean VALNAME
	define 	VERTICAL_V = 0, HORIZ_V = 1,     RATE_V = 2,
	        BFHOLD_V = 3,   BFRELEASE_V = 4, RADIUS_V = 5, STEPS_V = 6, HOLD_V = 7, RELEASE_V = 8;  // add as many as there are VALNAME[]

// Index to find Value Name string - switchable in game with left/right in ModEdit 
	int valName_idx;

// Adjustable values for [3] profiles: [profile_idx] = 0, 1 and 2 
	int antirecoil_vertical[3];
	int antirecoil_horizontal[3];
	int rounds_per_second[3];

// Adjustable values
	// BurstFire values
	int burstfire_hold; // BurstFire have toggle for each profiles BUT the same values for each
	int burstfire_release;
	
	// Sticky Assist values
	int aimassist_radius; // AIM Assist have toggle for each profiles BUT the same values for each
	int aimassist_angle;
	
	// AIM Abuse values
	int aimabuse_hold;
	int aimabuse_release;
	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

// Constant bytes - Convert numbers array - ASCII_NUM[column number]
	const uint8 ASCII_NUM[] = 
//	  0  1  2  3  4  5  6  7  8  9  (column numbers)
  	{48,49,50,51,52,53,54,55,56,57};
	
// Variables for functions 
	int i;

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

data(
//  0 Blue    1 Pink   2 SkyBlue  3 Green         
    1,0,0,0,  0,0,0,1,  1,0,1,0,  0,0,1,0,
//  4 Yellow  5 Orange  6 Red     7 White   8 OFF      
    0,1,1,0,  0,2,1,0,  0,1,0,0,  1,1,1,0,  0,0,0,0 );   
    int color;
         
// Refresh OLED
	int display_title = TRUE;
	int display_new; 
	int display_info;
	int display_black;
	int screensaver;

// ModMenu / ModEdit / KillSwitch
	int ModMenu;	
	int ModEdit;
	int KillSwitch;
	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 
	
// Counter
	int count_black; 
	int info_timer;
	
// Profiles
	int last_profile;
	int profile_idx; // Used to define Active Profiles and to find, edit, and display the right things
	
// HeadShot
	int head;
	int range_wait;
	int headshot_Y_val;
	define WAIT_CLOSE   = 160; // Change this value for close range
	define WAIT_MEDIUM  = 140; // Change this value for medium range
	define WAIT_LONG    = 120; // Change this value for long range	

// RapidFire
	int hold_time;                                                    
	int rest_time;
	int led_on;
	
//Misc
	int currentSlot;
	int current_info;
	int logo_on;
	int roll;
	int klax;
	int run;
	int vehicle;
	
	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

init{ 
					// Load default/stored values
// Profile 1
	// Toggles                                                  // Values
	toggle_antirecoil[0] = get_pvar(SPVAR_1,  0, 2, 0 );     	antirecoil_vertical[0]   = get_pvar(SPVAR_5,     -99,    99, 0);         
	toggle_rapidfire[0]  = get_pvar(SPVAR_2,  0, 1, 0 );        antirecoil_horizontal[0] = get_pvar(SPVAR_8,     -99,    99, 0);         
	toggle_burstfire[0]  = get_pvar(SPVAR_3,  0, 1, 0 );        rounds_per_second[0]     = get_pvar(SPVAR_9,       1,   25,  4);      
	toggle_aimassist[0]  = get_pvar(SPVAR_4,  0, 1, 0 ); 
	toggle_aimabuse[0]   = get_pvar(SPVAR_6,  0, 1, 0 );
	toggle_headshot[0]   = get_pvar(SPVAR_35, 0, 3, 0 );        

// Profile 2
	// Toggles                                                  // Values
	toggle_antirecoil[1] = get_pvar(SPVAR_10, 0, 2, 0 );    	antirecoil_vertical[1]   = get_pvar(SPVAR_14,    -99,    99, 0);         
	toggle_rapidfire[1]  = get_pvar(SPVAR_11, 0, 1, 0 );        antirecoil_horizontal[1] = get_pvar(SPVAR_17,    -99,    99, 0);         
	toggle_burstfire[1]  = get_pvar(SPVAR_12, 0, 1, 0 );        rounds_per_second[1]     = get_pvar(SPVAR_18,       1,   25,  4);        
	toggle_aimassist[1]  = get_pvar(SPVAR_13, 0, 1, 0 ); 
	toggle_aimabuse[1]   = get_pvar(SPVAR_15, 0, 1, 0 );        
	toggle_headshot[1]   = get_pvar(SPVAR_36, 0, 3, 0 );        

// Profile 3
	// Toggles                                                  // Values
	toggle_antirecoil[2] = get_pvar(SPVAR_19, 0, 2, 0 );        antirecoil_vertical[2]   = get_pvar(SPVAR_23,    -99,    99, 0);         
	toggle_rapidfire[2]  = get_pvar(SPVAR_20, 0, 1, 1 );        antirecoil_horizontal[2] = get_pvar(SPVAR_26,    -99,    99, 0);
	toggle_burstfire[2]  = get_pvar(SPVAR_21, 0, 1, 0 );        rounds_per_second[2]     = get_pvar(SPVAR_17,       1,   25,  4);
	toggle_aimassist[2]  = get_pvar(SPVAR_22, 0, 1, 0 ); 
	toggle_aimabuse[1]   = get_pvar(SPVAR_24, 0, 1, 0 );        
	toggle_headshot[2]   = get_pvar(SPVAR_37, 0, 3, 0 );        
	
// Misc Toggles 
	autorun_on   = get_pvar(SPVAR_28, 0, 1, 0 );
	rumble_on    = get_pvar(SPVAR_29, 0, 1, 0 );
	ceo_on       = get_pvar(SPVAR_34, 0, 1, 0 );
	
// Misc Values
	burstfire_hold       = get_pvar(SPVAR_30, 1,  1000, 120);
	burstfire_release    = get_pvar(SPVAR_31, 1,  1000, 120);
	aimassist_radius     = get_pvar(SPVAR_32, 1,    50,  20);
	aimassist_angle      = get_pvar(SPVAR_33, 1,  1000,  40);
	aimabuse_hold        = get_pvar(SPVAR_38, 1,  1000, 500);
	aimabuse_release     = get_pvar(SPVAR_39, 1,   500,  20);
	
// Get current slot
	currentSlot = get_slot();
}

main { // main section start
/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                         DEBUG                                                         |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/	

	set_val(TRACE_1, profile_idx);
	set_val(TRACE_2, modName_idx);
	set_val(TRACE_2, display_title);

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                       CROSSOVER                                                       |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/	 
    if(get_controller() != PIO_PS4) // If not PS4 controller:
    {
    	// If PS4 console:
        if(get_console() == PIO_PS4)
        { 
        	if(get_ival(PS4_SHARE))
        	{
        		if(get_ival(PS4_R3)) // RS + View =  ScreenShot
        		{
					set_val(PS4_SHARE,100);
                	set_val(PS4_TOUCH,0);
                	set_val(PS4_R3,0);
        		}
        		else // View only = PS4_TOUCH
        		{
        			set_val(PS4_TOUCH, 100);
        			set_val(PS4_SHARE, 0);
        		}
        	}
        }
	}

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                   TOGGLE KILLSWITCH                                                   |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/	 
	if(!ModMenu && !ModEdit)
	{
	// KillSwitch ON or OFF method
		if( (get_ival(PS4_L2) && event_press(PS4_SHARE)) || (!get_ival(PS4_L2) && event_press(PS4_PS)) )
		{			
			KillSwitch = !KillSwitch;
			
			if(KillSwitch) 
				combo_run(RUMBLE_OFF);	
				
			else
			{
				combo_run(RUMBLE_ON);
				display_title = TRUE;	
			}
		}
		
	// KillSwitch - exit method (FALSE, script is actived)
		if(KillSwitch) 
		{
    		screensaver= FALSE;
			count_black = 0;
			logo_on = FALSE;
	
			if(event_press(PS4_CIRCLE) && get_brtime(PS4_CIRCLE) < 200 ) // Double tap CIRCLE/B  to exit
			{
				KillSwitch = FALSE;
				combo_run(RUMBLE_ON);
				display_title = TRUE; 
			}
			combo_run(KILLSWITCH);
		}
		else 
			combo_stop(KILLSWITCH);
			
	// Block button	
		if(get_ival(PS4_L2))
		{
			if(get_controller() != PIO_PS4) 
				set_val(PS4_TOUCH, 0);
			else
				set_val(PS4_SHARE, 0);
		}
	}

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                        IF KILLSWITCH IS OFF (SCRIPT IS ACTIVE)                                        |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/	
	if(!KillSwitch)
	{
	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|                                     TOGGLE MOD MENU                                   |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
		if(get_ival(PS4_L2))
		{	
			if(event_release(PS4_OPTIONS))
			{
				cls_oled(OLED_BLACK);
    			logo_on = FALSE;
    			
				if(ModEdit)
				{
					ModEdit = FALSE;
					display_title = TRUE;   
				}
				else 
					ModMenu = !ModMenu;
				
				if(!ModMenu)
				{
					profile_idx = last_profile;
					combo_run(RUMBLE_OFF); 
					display_title = TRUE;  
				}
				if(ModMenu)
				{
					last_profile = profile_idx;
					profile_idx = 0;
					modName_idx = ANTIRECOIL_M;        
					current_info = 0;		
					combo_run(RUMBLE_ON);	
					draw_base_menu();		
					draw_modMenu();		
					draw_chars_line1();		
					draw_chars_line2(); 
					draw_chars_line3();		
					display_new = TRUE;
					display_info = TRUE;
				}
			}
			set_val(PS4_OPTIONS, 0);
		} // L2/LT section end

	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|                                   MOD MENU IS ACTIVE                                  |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
		if(ModMenu) 
		{
		// Buttons animation
			button_animation(); 
		/*— ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤
		|                         TOGGLE FOR EDIT MENU                          |
		¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ —*/
		// From ModMenu to ModEdit
			if(check_toggle()) // if toggle is TRUE, I can enter Edit Menu	 
			{
				if(event_release(PS4_CROSS))
				{
					current_info = 0;		
					ModEdit = TRUE;
					ModMenu = FALSE;
					draw_modEdit();			
					draw_chars_line1();		
					draw_chars_line2();		
					draw_hold_to_edit();
					draw_chars_line3();	
					display_new = TRUE;
					display_info = TRUE;
				}
			}
		/*— ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — °
		|                  MOD NAME NAVIGATION                  |
		° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° —*/
		// Navigate mods name
			if(event_press(PS4_RIGHT))
			{
				modName_idx = cycle(modName_idx + 1, 0, entry_count(MODNAME_TXT)); // create a loop in the values
				display_new = TRUE; 
			}
			
			if(event_press(PS4_LEFT))
			{
				modName_idx = cycle(modName_idx - 1, 0, entry_count(MODNAME_TXT));  // create a loop in the values
				display_new = TRUE; 
			}
			
		/*— ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — °
		|                    TOGGLE FOR MODS                    |
		° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° —*/
		// Tap UP or DOWN to turn ON/OFF
	   		// Mods that may have different ON/OFF status depending on the active Profile
			toggle_antirecoil[profile_idx]    = toggle( ANTIRECOIL_M   , toggle_antirecoil[profile_idx]    );
			toggle_rapidfire[profile_idx]     = toggle( RAPIDFIRE_M    , toggle_rapidfire[profile_idx]     );
			toggle_burstfire[profile_idx]     = toggle( BURSTFIRE_M    , toggle_burstfire[profile_idx]     );
			toggle_aimassist[profile_idx]     = toggle( AIMASSIST_M    , toggle_aimassist[profile_idx]     );
			toggle_aimabuse[profile_idx]      = toggle( AIM_ABUSE_M    , toggle_aimabuse[profile_idx]      );
			toggle_headshot[profile_idx]      = toggle( HEADSHOT_M     , toggle_headshot[profile_idx]      );
		
		// Mods that have same ON/OFF status on every Profiles
			autorun_on     = toggle( AUTORUN_M       , autorun_on     );
			rumble_on      = toggle( RUMBLEFEEDBACK_M, rumble_on      );
			ceo_on         = toggle( CEO_M           , ceo_on         );
		} // ModMenu section end
	
	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|                                  EDIT MENU IS ACTIVE                                  |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/	
		if(ModEdit) 
		{
		// Buttons animation
			button_animation(); // define if the rectangle behind the arrow must be clear or fill, and define if the arrow must be black or white
			
		/*— ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — °
		|                EDIT ADJUSTABLE VALUES                 |
		° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° —*/
		// Edit value: hold CROSS/A + UP (+1 or +10) or DOWN (-1 or -10) or RIGHT (+10 or +100) or LEFT (-10 or -100)	
			if(get_ival(PS4_CROSS)) 
			{
			// Mods that may have different values depending on the active Profile                           val min, val max, inc/dec R/L, increase/decrease UP/DOWN 
				antirecoil_vertical[profile_idx]   = edit_val( VERTICAL_V , antirecoil_vertical[profile_idx]  ,    -99 ,     99 ,          1 ,          10               ); 
				antirecoil_horizontal[profile_idx] = edit_val( HORIZ_V    , antirecoil_horizontal[profile_idx],    -99 ,     99 ,          1 ,          10               ); 
				rounds_per_second[profile_idx]     = edit_val( RATE_V     , rounds_per_second[profile_idx]    ,      1 ,     25 ,          1 ,          10               );
				
			// Mods that have same value on every Profiles
				burstfire_hold         = edit_val( BFHOLD_V    , burstfire_hold         , 0 ,  1000 , 10 , 100 );
				burstfire_release      = edit_val( BFRELEASE_V , burstfire_release      , 0 ,  1000 , 10 , 100 );
				aimassist_radius       = edit_val( RADIUS_V    , aimassist_radius       , 0 ,    50 ,  1 ,  10 );
				aimassist_angle        = edit_val( STEPS_V     , aimassist_angle        , 0 ,    50 ,  1 ,  10 ); 
				aimabuse_hold          = edit_val( HOLD_V      , aimabuse_hold          , 0 ,  1000 , 10 , 100 ); 
				aimabuse_release       = edit_val( RELEASE_V   , aimabuse_release       , 0 ,   500 , 10 , 100 ); 
			} // CROSS/A section end
			
		/*— ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — °
		|  FIND AVAILABLE VALUES AND NAVIGATION BETWEEN VALUES  |
		° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° — ° —*/
			else // if NOT EDIT_BTN
			{	
			// From ModEdit to ModMenu	
				if(event_press(PS4_CIRCLE))
				{
					current_info = 0;	
					ModMenu = TRUE;
					ModEdit = FALSE;
					draw_modMenu();		
					draw_chars_line1();	
					draw_chars_line2();	
					draw_chars_line3();
					display_new = TRUE;
					display_info = TRUE;
				}
				
			// Navigate value name
				if(event_press(PS4_RIGHT))
				{
					valName_idx += 1;
					display_new = TRUE; 
				}
				if(event_press(PS4_LEFT))
				{
					valName_idx -= 1;
					display_new = TRUE; 
				}
					
			// Available values for each of the mods 
				if(modName_idx == ANTIRECOIL_M)  valName_idx = cycle(valName_idx, VERTICAL_V , HORIZ_V     ); // from START_V to HORIZ_V
				if(modName_idx == RAPIDFIRE_M)   valName_idx = cycle(valName_idx, RATE_V     , RATE_V      ); // RATE_V only
				if(modName_idx == BURSTFIRE_M)   valName_idx = cycle(valName_idx, BFHOLD_V   , BFRELEASE_V ); // from BFHOLD_V to BFRELEASE_V
				if(modName_idx == AIMASSIST_M)   valName_idx = cycle(valName_idx, RADIUS_V   , STEPS_V     ); // from RADIUS_V to STEPS_V
				if(modName_idx == AIM_ABUSE_M)   valName_idx = cycle(valName_idx, HOLD_V     , RELEASE_V   ); // from HOLD_V to RELEASE_V
			} // NOT EDIT_BTN section end
		} // ModEdit section end	

	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|                             MOD MENU OR MOD EDIT IS ACTIVE                            |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
		if(ModMenu || ModEdit) 
		{
		//Stop screen Saver
    		screensaver = FALSE;
			count_black = 0;
			
		// Timer to display user infos
			info_timer += get_rtime();
	
			if(info_timer > 2000)
			{
				current_info ++;
				display_info = TRUE;
			}
			
		// Save everything and EXIT
			if(!get_ival(PS4_L2) && event_release(PS4_PS))
			{
				save();
			}
				
		// block ALL inputs 
			block_btn(); // user function
			
		// Switch Profiles (not in ModEdit)
			if(event_release(PS4_TRIANGLE) && !ModEdit)
			{	
				profile_idx = cycle(profile_idx + 1, 0, entry_count(PROFILE_TXT));
				display_new = TRUE; 
			}
		}
				
	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|                             FEATURES ACTIVE IN GAME ONLY                              |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
		if(!ModMenu && !ModEdit)
		{
		/*— ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤
		|                               FEATURES                                |
		¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ —*/
		
			// Block controller rumbles
				if(!rumble_on)
					block_rumble();
					
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

			// Vehicle Mode
				if(event_press(PS4_TRIANGLE) || event_press(PS4_DOWN) && get_brtime(PS4_DOWN) < 200)
					vehicle = !vehicle;
					
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

		// Fast Snack, Armor, Sparrow and Personal Vehicle
			if(get_val(PS4_CIRCLE))
			{
				if(event_press(PS4_RIGHT))
					combo_run(SNACK);
					
				if(event_press(PS4_LEFT))
					combo_run(ARMOR);
					
				if(event_press(PS4_DOWN))
					combo_run(SPAROW);
					
				if(event_press(PS4_UP))
					combo_run(PERSONAL_VEHICLE);
					
				set_val(PS4_RIGHT, 0); set_val(PS4_LEFT, 0);
				set_val(PS4_DOWN, 0); set_val(PS4_UP, 0);
			}
			
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

	    // Klaxon
	    	if(get_ival(PS4_R2))
	    	{
	    		if(get_ival(PS4_L3) && get_ptime(PS4_L3) > 500)
	    			klax = TRUE;
	    	}
	    	if(klax)
	    	{
	    		set_val(PS4_L3, 100);
	    		
	    		if(event_press(PS4_L3) || event_press(PS4_CROSS))
	    			klax = FALSE;
	    	}
	    	
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

			if(!vehicle)
			{
			// Switch Profiles 
				if(get_val(PS4_L1) && isqrt(pow(abs(get_ival(PS4_RX)),2) + pow(abs(get_ival(PS4_RY)),2)) >= 90)
				{
					if(get_val(PS4_RX) > 38)
					{
						if(get_val(PS4_RY) < -38)  
							profile_idx = 1; // 1h30		
						else if(get_val(PS4_RY) > 38)  
							profile_idx = 3; // 4h30
						else
							profile_idx = 2; // 3h00
					}	
				
					else if(get_val(PS4_RX) < -38)
					{
						if(get_val(PS4_RY) < -38)  
							profile_idx = 3; // 10h30		
						else if(get_val(PS4_RY) > 38)  
							profile_idx = 3; // 7h30
						else
							profile_idx = 3; // 9h00
					}
					
					else
					{
						if(get_val(PS4_RY) > 38)
							profile_idx = 3; // 6h00
						else if(get_val(PS4_RY) < -38)
							profile_idx = 0; // 12h00
					}
					last_profile = profile_idx;
				}
				else if(event_press(PS4_L1))
				{
					if(profile_idx == 3)
						profile_idx = last_profile;
					else 
						profile_idx = 3;
				}
				if(event_release(PS4_L1))
					display_title = TRUE;
						
	// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
	
			// Easy Roll
				if(get_ival(PS4_L3) && get_ptime(PS4_L3) >= 500)
		    		roll = TRUE;
		    	else
		    		roll = FALSE;
		    			
		    	if(isqrt(pow(abs(get_ival(PS4_LX)),2) + pow(abs(get_ival(PS4_LY)),2)) >= 50) 
		    	{
		    		if(!get_ival(PS4_L2) && event_press(PS4_L3) || roll)
		    			combo_run(EASY_ROLL);
		    			
		    		set_val(PS4_L3, 0);
		    	}
				
	// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
	
	    	// RockStar AutoRun
		    	if(autorun_on)
		    	{
					if(isqrt(pow(abs(get_ival(PS4_LX)),2) + pow(abs(get_ival(PS4_LY)),2)) >= 20)
					{
						// walk -> run -> sprint -> walk
						if(event_press(PS4_CROSS))
							run += 1;
						if(run > 2)
							run = 0;
							
						if(!get_ival(PS4_CROSS))
						{
						// run
							if(run == 1) 
								set_val(PS4_CROSS, 100);
						// sprint
							if(run == 2)
		        				combo_run(SPAM);
						}
					}
					// reset
					else
						run = 0;
				}
				
	// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
				
			// Speed Toggles
				autorun_on  = speed_toggle( PS4_L2, PS4_UP   , autorun_on , AUTORUN_M );
				ceo_on      = speed_toggle( PS4_L2, PS4_DOWN , ceo_on     , CEO_M     );
				
				if(profile_idx < 3)
				{
					toggle_aimabuse[profile_idx] = speed_toggle( PS4_L2, PS4_RIGHT, toggle_aimabuse[profile_idx] , AIM_ABUSE_M );
					toggle_headshot[profile_idx] = speed_toggle( PS4_L2, PS4_LEFT , toggle_headshot[profile_idx] , HEADSHOT_M  );
			
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
				
				// Sticky Assist
					if(toggle_aimassist[profile_idx])
					{
						if(get_val(PS4_L2) || get_val(PS4_R2) && (isqrt(pow(abs(get_ival(PS4_RY)),2) + pow(abs(get_ival(PS4_RX)),2)) < 100) )
							combo_run(STICKY);
					}
					
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
				
				// AIM Abuse
					if(toggle_aimabuse[profile_idx])
					{
						if(get_val(PS4_L2))
							combo_run(AIM_ABUSE);
						else
							combo_stop(AIM_ABUSE);
					}
					
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
				// AntiRecoil
					if(toggle_antirecoil[profile_idx])
					{	
						if(get_val(PS4_R2))
						{
						// If BurstFire
							if(toggle_burstfire[profile_idx] == 1 && get_ptime(PS4_R2) <= hold_time)
							{
								antirecoil();
							}
						// Full auto and RapidFire with smart stop
						    if(toggle_burstfire[profile_idx] == 0  || toggle_rapidfire[profile_idx] >= 1)
						    {
								antirecoil();
							}
						}
					}
		
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
		
			  	// If Head Shot is enable for the curent profile
					if(toggle_headshot[profile_idx])
					{
					if(!get_val(PS4_L1) && get_val(PS4_L2))
					{
						if(antirecoil_vertical[profile_idx] < 0)
							headshot_Y_val = 100;
						else
							headshot_Y_val = -100;
					}
					
					if(toggle_headshot[profile_idx] == 1)
						range_wait = WAIT_CLOSE;
					if(toggle_headshot[profile_idx] == 2)
						range_wait = WAIT_MEDIUM;
					if(toggle_headshot[profile_idx] == 3)
						range_wait = WAIT_LONG;
					
					if(!get_val(PS4_L1) && (get_ival(PS4_L2) && get_ptime(PS4_L2) > 300 && !head))
					{
						combo_run(HEADSHOT);
						head = TRUE;
					}	
					
					if(event_release(PS4_L2) && get_ptime(PS4_L2) > 300)
					{
						headshot_Y_val = (3 * (headshot_Y_val / 5)) * -1;
						combo_run(HEADSHOT);
						head = FALSE;
					}
				}
		
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
		
				// If RapidFire is enable for the active profile
					if(toggle_rapidfire[profile_idx])
					{
					// Rate of Fire Calculation
			    		hold_time = 500 / rounds_per_second[profile_idx]; 
			   			rest_time = hold_time;
			    		if(rest_time < 0) rest_time = 0;
			    
					// Run combo
			    		if(!get_val(PS4_L1))
			    		{
			    			if(get_val(PS4_L2) && get_val(PS4_R2))
			    				combo_run(RAPIDFIRE);
			    			else
			    				combo_stop(RAPIDFIRE);
			    		}
					}
					
		// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
				
				//BurstFire
					if(toggle_burstfire[profile_idx]) 
					{
			    	// Hold and Release
						hold_time = burstfire_hold;
						rest_time = burstfire_release;
						
			    	// Run combo
			    		if(get_val(PS4_R2))	
			    			combo_run(BURSTFIRE);
						else
			    			combo_stop(BURSTFIRE);
			
			    		led_on = 200;
			    		combo_run(BLINK);
					}
				}
			}

	/*— ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤
	|                          SCREEN SAVER BY DoGz                         |
	¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ — ¤ —*/
		    if(display_black)
		    {
		    	count_black += get_rtime();
		    	if(count_black >= 5000)
		    	{
		    		cls_oled(0);
		    		count_black = 0;
		    		screensaver = TRUE;
		    		logo_on = FALSE;
		    		display_black = FALSE;
		    	}
		    }
		    
		    if(screensaver)
		    {
		    	DGT += get_rtime();
				if(DGT >= 100) 
				{
					SD1 += 2;	TP1 += 1;	TP2 -= 1;	DGT  = 0;
				}
				DGR += get_rtime();
				
				if(DGR >= 30) 
				{
					Y_1 += 1;	Y_2 += 2;	Y_3 += 3;	Y_4 += 4;
					Y_5 += 5;	Y_6 += 6;	Y_7 += 7;	Y_8 += 8;
					DGR  = 0;
				}
				
				name();
				MSC += get_rtime();
				
				if(MSC >= 1000) 
				{
					MSC = 0;	
					RT += 1;
				}
				
				if(RT == 30) 
				{
					screensaver = 0;
					RT = 0;
					cls_oled(0);
				}
		    }
		} // NOT ModMenu and NOT ModEdit section end
		

	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|            DISPLAY TEXT AND VALUES IN MOD MENU, MOD EDIT AND MOD WEAPON               |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
		if(display_new) 
		{
			screensaver = FALSE;
			count_black = 0;
			
			if(ModEdit) // Display editable value 
			{
				display_edit_line1();
			// Clears value zone
				rect_oled(38, 18, 51 , OLED_FONT_SMALL_HEIGHT + 3 , OLED_WHITE , OLED_BLACK); // small rectangle left (line 2 value)
				rect_oled(27, 31, 74 , OLED_FONT_SMALL_HEIGHT + 6 , OLED_WHITE , OLED_BLACK); // small rectangle left (line 3 value)
			
			// Display editable value
				display_values();
			}
			else if(ModMenu) // Display Profile, mod name, toggle
			{
				display_edit_line1();
				display_edit_line2();
				display_edit_line3();
			}
			else // Speed Toggle Display
			{
				cls_oled(OLED_BLACK);
			// Display mod name
				print(center_x(get_string_length(get_string(MODNAME_TXT, modName_idx)), OLED_FONT_SMALL_WIDTH),LINE_2_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(MODNAME_TXT, modName_idx));
			// Display toggle
				display_edit_line3();
				count_black = 2000;
			}
			
			display_black = TRUE;
	        display_new = FALSE;
	       
	    } // display_new section end

	/*— x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x
	|               DISPLAY USER INFOS IN MOD MENU, MOD EDIT AND MOD WEAPON                 |
	x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x — x —*/
	
		if(display_info) // display navigation infos
		{
			info_timer = 0;
			rect_oled(1, 50, 126, 13, OLED_WHITE, OLED_BLACK); // bottom rectangle (clear)
			if(ModMenu)
			{
				current_info = cycle(current_info, 0, entry_count(INFO_MENU_PS_TXT));
				
				if(get_controller() == PIO_PS4)
					print(center_x(get_string_length(get_string(INFO_MENU_PS_TXT, current_info)), OLED_FONT_SMALL_WIDTH),LINE_4_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(INFO_MENU_PS_TXT, current_info));
				else
					print(center_x(get_string_length(get_string(INFO_MENU_XB_TXT, current_info)), OLED_FONT_SMALL_WIDTH),LINE_4_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(INFO_MENU_XB_TXT, current_info));
			}
			if(ModEdit)
			{
				current_info = cycle(current_info, 0, entry_count(INFO_EDIT_PS_TXT));
				
				if(get_controller() == PIO_PS4)
					print(center_x(get_string_length(get_string(INFO_EDIT_PS_TXT, current_info)), OLED_FONT_SMALL_WIDTH),LINE_4_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(INFO_EDIT_PS_TXT, current_info));
				else
					print(center_x(get_string_length(get_string(INFO_EDIT_XB_TXT, current_info)), OLED_FONT_SMALL_WIDTH),LINE_4_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(INFO_EDIT_XB_TXT, current_info));
			}
			display_info = FALSE;
		}
	} // !KillSwitch section end

// back to main section

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                           DISPLAY TITLE - KILLSWITCH - PROFILE - WEAPON NAME - # SLOT                                 |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/
// Display Script Title    
    if(display_title)
    {
    	screensaver= FALSE;
    	count_black = 0;	
    	
    // Display logo
    	if(!logo_on)
    	{
    		cls_oled(0);
   			draw_logo();
   		}
   		else
   			rect_oled(0, 45, 128, 31, OLED_WHITE, OLED_BLACK); // clear profile and #slot
   			
   	// Display current slot
   		number_to_string(currentSlot, find_digits(currentSlot)); 

   	// Display profile name
		display_profile();
		
		logo_on = TRUE;
   		display_black = TRUE;	
    	display_title = FALSE;
    }

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                      COLOR LED                                                        |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/
	if(toggle_headshot[profile_idx])
	{
		if(!toggle_rapidfire[profile_idx])
			led_on = 2000;
		else
			led_on = 300;
		
		color = toggle_headshot[profile_idx] + 3;
		combo_run(BLINK);
	}
	else 
	{	
		if(toggle_rapidfire[profile_idx])
		{
			led_on = 300;
			color = 8;
			combo_run(BLINK);
		}
	}
	if(KillSwitch)
		led(8);
	else if(vehicle)
		led(6);
	else
		led(profile_idx);
		
	
		
} // main section end
/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                  MAIN SECTION END                                                     |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                   COMBO SECTION                                                       |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/
combo SNACK {
	call(TOUCHPAD);
	call(DOWN);
	call(DOWN);
	if(ceo_on)
		set_val(PS4_DOWN,100);
	wait(50);
	wait(50);
	call(CROSS);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	call(DOWN);	
	call(CROSS);
	call(DOWN);	
	call(CROSS);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo ARMOR {
	call(TOUCHPAD);
	call(DOWN);
	call(DOWN);
	if(ceo_on)
		set_val(PS4_DOWN,100);
	wait(50);
	wait(50);
	call(CROSS);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	call(CROSS);
	call(UP);
	call(UP);
	call(UP);
	call(CROSS);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo SPAROW {
	call(TOUCHPAD);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	if(ceo_on)
		set_val(PS4_DOWN,100);
	wait(50);
	wait(50);
	call(CROSS);
	call(UP);
	call(CROSS);
	call(DOWN);
	call(CROSS);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo PERSONAL_VEHICLE {
	call(TOUCHPAD);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	call(DOWN);
	if(ceo_on)
		set_val(PS4_DOWN,100);
	wait(50);
	wait(50);
	call(CROSS);
	call(CROSS);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo TOUCHPAD {
	set_val(PS4_TOUCH,100);
	wait(400);
	wait(150);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo DOWN {
	set_val(PS4_DOWN,100);
	wait(50);
	wait(50);
	
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo UP {
	set_val(PS4_UP,100);
	wait(50);
	wait(50);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo CROSS {
	set_val(PS4_CROSS,100);
	wait(50);
	wait(250);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo EASY_ROLL {	
	set_val(PS4_L2,100);
	set_val(PS4_LX,get_ival(PS4_LX));
	set_val(PS4_LY,get_ival(PS4_LY));	
	set_val(PS4_SQUARE,100);
	wait(80);
	set_val(PS4_LX,get_ival(PS4_LX));
	set_val(PS4_LY,get_ival(PS4_LY));	
	wait(400);
	wait(1500);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo SPAM { 
    set_val(PS4_CROSS, 100);
    wait(80);
    set_val(PS4_CROSS, 0);
    wait(80);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo HEADSHOT { 
    set_val(PS4_RY, headshot_Y_val);
    wait(range_wait);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo BURSTFIRE { 
    wait(hold_time);
	call(RAPIDFIRE);
	call(RAPIDFIRE);
	call(RAPIDFIRE);
	call(RAPIDFIRE);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo RAPIDFIRE { 
    wait(hold_time);
    set_val(PS4_R2, 0);
    wait(rest_time - get_rtime());
    set_val(PS4_R2, 0);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo STICKY {
	if(toggle_antirecoil[profile_idx] && get_val(PS4_R2))
    	set_val(PS4_RY, SmartSens(antirecoil_vertical[profile_idx] + aimassist_radius, PS4_RY));
    else 
    	set_val(PS4_RY, SmartSens(aimassist_radius, PS4_RY));
    wait(aimassist_angle);
    
	if(toggle_antirecoil[profile_idx] && get_val(PS4_R2))
    	set_val(PS4_RY, SmartSens(antirecoil_vertical[profile_idx], PS4_RY));
    set_val(PS4_RX, SmartSens(aimassist_radius, PS4_RX));
    set_val(PS4_LX, SmartSens(aimassist_radius, PS4_LX));
    wait(aimassist_angle);
    
	if(toggle_antirecoil[profile_idx] && get_val(PS4_R2))
    	set_val(PS4_RY, SmartSens(antirecoil_vertical[profile_idx] + inv(aimassist_radius), PS4_RY));
    else
    	set_val(PS4_RY, SmartSens(inv(aimassist_radius), PS4_RY));
    wait(aimassist_angle);
    
	if(toggle_antirecoil[profile_idx] && get_val(PS4_R2))
    	set_val(PS4_RY, SmartSens(antirecoil_vertical[profile_idx], PS4_RY));
    set_val(PS4_RX, SmartSens(inv(aimassist_radius), PS4_RX));
    set_val(PS4_LX, SmartSens(inv(aimassist_radius), PS4_LX));
    wait(aimassist_angle - get_rtime());
    
	if(toggle_antirecoil[profile_idx] && get_val(PS4_R2))
    	set_val(PS4_RY, SmartSens(antirecoil_vertical[profile_idx], PS4_RY));
    set_val(PS4_RX, SmartSens(inv(aimassist_radius), PS4_RX));
    set_val(PS4_LX, SmartSens(inv(aimassist_radius), PS4_LX));
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo AIM_ABUSE {
    wait(aimabuse_hold);
    set_val(PS4_L2, 0);
    wait(aimabuse_release - get_rtime());
    set_val(PS4_L2, 0);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo RACE {
	X_1 = random(0,127);
	wait(SAVER1);
	X_2 = random(0,127);
	wait(SAVER1);
	X_3 = random(0,127);
	wait(SAVER1);
	X_4 = random(0,127);
	wait(SAVER1); 
	X_5 = random(0,127);
	wait(SAVER1);
	X_6 = random(0,127);
	wait(SAVER1); 
	X_7 = random(0,127);
	wait(SAVER1); 
	X_8 = random(0,127);
	wait(SAVER1);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo RUMBLE_ON {					
	set_ledx(2, 2);
	set_rumble(RUMBLE_B, 50);
	wait(150);
	reset_rumble();
	wait(100);
	set_rumble(RUMBLE_B, 50);
	wait(150);
	reset_rumble();
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo RUMBLE_OFF {						
	set_ledx(1, 1);
	set_rumble(RUMBLE_A, 50);
	wait(300);
	reset_rumble();
	wait(400);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜
	
combo KILLSWITCH {
	cls_oled(OLED_BLACK);
    print(center_x(sizeof(MUTE) - 1, OLED_FONT_MEDIUM_WIDTH),8,OLED_FONT_MEDIUM,OLED_WHITE,MUTE[0]);    
    wait(get_rtime());
    wait(250);
    print(center_x(sizeof(HOLD) - 1, OLED_FONT_SMALL_WIDTH),38,OLED_FONT_SMALL,OLED_WHITE,HOLD[0]);     
    print(center_x(sizeof(ACTIVE) - 1, OLED_FONT_SMALL_WIDTH),50,OLED_FONT_SMALL,OLED_WHITE,ACTIVE[0]); 
    wait(get_rtime());
    wait(5000);  
    cls_oled(OLED_BLACK);
    wait(get_rtime());
    wait(5000);  
}
    
// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

combo SAVE { 
	call(RUMBLE_ON);
	wait(150);
	call(RUMBLE_ON);		
	wait(1000);
	display_title = TRUE;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo RN {
	RN1 = random(65,90);
	RN2 = random(60,90);
	RN3 = random(65,90);
	RN4 = random(65,90); 
	RN5 = random(65,90);
	RN6 = random(65,90);
	RN7 = random(65,90);
	RN8 = random(65,90);
	wait(SAVER);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

combo BLINK {
    wait(led_on);
    led(color);
    wait(300);
}

/*— ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌
|                                                 FUNCTION SECTION                                                      |
◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ — ◌ —*/
int SmartVal;	

function SmartSens(f_val, f_axis) { 

	SmartVal = ( (f_val * ( 100 - abs( get_val(f_axis) ) ) ) / 100); 
    return clamp(get_val(f_axis) + SmartVal, -100, 100);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function antirecoil() {

	if(!toggle_aimassist[profile_idx])
	{
		set_val(PS4_RY, clamp(SmartSens(antirecoil_vertical[profile_idx], PS4_RY), -100, 100));   
		set_val(PS4_RX, clamp(SmartSens(antirecoil_horizontal[profile_idx], PS4_RX), -100, 100));
	}
	else if(toggle_aimassist[profile_idx])
	{
		combo_run(STICKY);
	}
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function display_edit_line1() {

// Clears text emplacement line 1
	rect_oled(13, 1, 102, OLED_FONT_SMALL_HEIGHT + 5, OLED_WHITE , OLED_BLACK);
	
	if(ModMenu)
	{				
	// Display Profile only on mods that may have different ON/OFF status depending on the active Profile
		if(modName_idx < AMOUNT_OF_MULTI_TOGGLE) 
			display_profile ();
		else // Mods that have the same toggle on every profiles, display EVERY PROFILE
    		print(center_x(sizeof(ALL) - 1, OLED_FONT_SMALL_WIDTH),4,OLED_FONT_SMALL,OLED_WHITE,ALL[0]);
	}
	
	if(ModEdit) 
	{
	// Display Value Name
    	print(center_x(get_string_length(get_string(VALNAME_TXT, valName_idx)), OLED_FONT_SMALL_WIDTH),LINE_1_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(VALNAME_TXT, valName_idx));  
	}
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function display_edit_line2() {
	
// Clears text emplacement line 2
	rect_oled(13, 17, 102, OLED_FONT_SMALL_HEIGHT + 5, OLED_WHITE , OLED_BLACK);
		
	if(ModMenu)
	{
	// Display Mod Name
		print(center_x(get_string_length(get_string(MODNAME_TXT, modName_idx)), OLED_FONT_SMALL_WIDTH),LINE_2_Y,OLED_FONT_SMALL,OLED_WHITE,get_string(MODNAME_TXT, modName_idx));  // small size
	}
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function display_edit_line3() {
	
// Clears text emplacement line 3
	rect_oled(13, 33, 102, OLED_FONT_SMALL_HEIGHT + 5, OLED_WHITE , OLED_BLACK);

// Display toggle ON/OFF status
    // Mods that may have different ON/OFF status depending on the active Profile
	display_toggle( ANTIRECOIL_M    , toggle_antirecoil[profile_idx]   );
	display_toggle( RAPIDFIRE_M     , toggle_rapidfire[profile_idx]    );
	display_toggle( BURSTFIRE_M     , toggle_burstfire[profile_idx]    );
	display_toggle( AIMASSIST_M     , toggle_aimassist[profile_idx]    );
	display_toggle( AIM_ABUSE_M     , toggle_aimabuse[profile_idx]     );
	display_toggle( HEADSHOT_M      , toggle_headshot[profile_idx]     );
	
	// Mods that have same ON/OFF status on every Profile
	display_toggle( AUTORUN_M        , autorun_on    );
	display_toggle( RUMBLEFEEDBACK_M , rumble_on     );
	display_toggle( CEO_M            , ceo_on        );
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function display_values() {

	// Mods that may have different values depending on the active Profile
	display_edit( VERTICAL_V , antirecoil_vertical[profile_idx]   );
	display_edit( HORIZ_V    , antirecoil_horizontal[profile_idx] );
	display_edit( RATE_V     , rounds_per_second[profile_idx]     );
				
	// Mods that have same value on every Profile
	display_edit( BFHOLD_V    , burstfire_hold    );
	display_edit( BFRELEASE_V , burstfire_release );
	display_edit( RADIUS_V    , aimassist_radius  );
	display_edit( STEPS_V     , aimassist_angle   );
	display_edit( HOLD_V      , aimabuse_hold     );
	display_edit( RELEASE_V   , aimabuse_release  );
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int profile_y;

function display_profile() {

	if(display_title)
		profile_y = 50; // Title screen
	else
		profile_y = 4; // Menus
	
    print(center_x(get_string_length(get_string(PROFILE_TXT, profile_idx)), OLED_FONT_SMALL_WIDTH),profile_y,OLED_FONT_SMALL,OLED_WHITE,get_string(PROFILE_TXT, profile_idx));
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

function display_edit(f_idx, f_val) { 
	
    if(valName_idx == f_idx)
    		number_to_string(f_val, find_digits(f_val));
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function display_toggle(f_idx, f_toggle) { 

    if(modName_idx == f_idx)
   	{
   		if(f_idx == HEADSHOT_M)
   		{
   		// Display HeadShot Range
	    	if(f_toggle == 1)
	    		print(center_x(sizeof(CLOSE) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,CLOSE[0]);
	    	else if(f_toggle == 2)
	    		print(center_x(sizeof(MID) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,MID[0]);
	    	else if(f_toggle == 3)
	    		print(center_x(sizeof(LONG) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,LONG[0]);
	    	else
	    		print(center_x(sizeof(OFF) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,OFF[0]);
   		}
   		else
   		{
	    // Display ON OFF
	    	if(f_toggle == 1)
	    		print(center_x(sizeof(ON) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,ON[0]);
	    	else
	    		print(center_x(sizeof(OFF) - 1, OLED_FONT_SMALL_WIDTH),LINE_3_Y,OLED_FONT_SMALL,OLED_WHITE,OFF[0]);
    	}
   	}
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

define SSA1 = 78,SSA2 = 79,SSA3 = 90,SSA4 = 66,SSA5 = 65,SSA6 = 82,SSA7 = 68,SSA8 = 82,SSA9 = 73,SSA10 = 70,SSA11 = 82,SSA13 = 50,SSA14 = 49,SAVER = 5000,SAVER1 = 10;

function edit_val(f_idx, f_val, f_min, f_max, f_inc1, f_inc2) {  
	
	if(valName_idx == f_idx)
	{
    // +1 or -1															 
    	f_val    = value_change( f_val ,PS4_LEFT , inv(f_inc1), f_min, f_max ); 
    	f_val    = value_change( f_val ,PS4_RIGHT, f_inc1     , f_min, f_max ); 
   	 // +10 or -10
   	   	f_val    = value_change( f_val ,PS4_DOWN , inv(f_inc2), f_min, f_max ); 
       	f_val    = value_change( f_val ,PS4_UP   , f_inc2     , f_min, f_max ); 
	}		
	return f_val;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function value_change(f_val,f_btn,f_inc, f_rng_min, f_rng_max) { 

    if(press_hold(f_btn)) 
    {
        f_val += f_inc;
	// Substract    
	  	if(f_val < f_rng_min) 
	  		f_val = f_rng_min; 
	// Add   
	  	if(f_val > f_rng_max) 
	  		f_val = f_rng_max; 
    		
        display_new = TRUE; 
    }
    return f_val; 
} 

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function toggle(f_idx, f_val) { 
	
	if(event_press(PS4_UP))
	{
		if(modName_idx == f_idx)
		{
			if(f_idx == HEADSHOT_M)
				f_val = cycle(f_val + 1, 0, 3);
			else
				f_val = cycle(f_val + 1, 0, 1);
		
		// Rumble	
			if(f_val == 0) 
				combo_run(RUMBLE_OFF);
			else
				combo_run(RUMBLE_ON);
		}
		display_new = TRUE; 
	}

	if(event_press(PS4_DOWN))
	{
		if(modName_idx == f_idx)
		{
			if(f_idx == HEADSHOT_M)
				f_val = cycle(f_val - 1, 0, 3);
			else
				f_val = cycle(f_val - 1, 0, 1);
		
		// Rumble	
			if(f_val == 0) 
				combo_run(RUMBLE_OFF);
			else
				combo_run(RUMBLE_ON);	
		}
		display_new = TRUE; 
	}
				
	if(f_idx == RAPIDFIRE_M)
	{
		if(f_val == 1)
			toggle_burstfire[profile_idx] = 0;
	}
		
	if(f_idx == BURSTFIRE_M)
	{
		if(f_val == 1)
			toggle_rapidfire[profile_idx] = 0;
	}
	
	return f_val;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function speed_toggle(f_hold, f_press, f_val, f_idx) {  
	
	if(get_val(f_hold))
	{
		if(get_val(f_press) && get_ptime(f_press) == 500) // Hold second button 300ms or more
		{
			modName_idx = f_idx;
			f_val = !f_val;	
			
			if(f_val == 0)
				combo_run(RUMBLE_OFF);
			else
				combo_run(RUMBLE_ON);
				
			display_new = TRUE; 
		} 
		
		if(f_idx == HEADSHOT_M)
		{
			if(event_press(f_press) && get_brtime(f_press) < 200)
			{
				modName_idx = f_idx;
				f_val = cycle(f_val++, 1, 3);
				display_new = TRUE; 
			}
		}
		
		set_val(f_press, 0)
	}
				
	if(f_idx == RAPIDFIRE_M)
	{
		if(f_val == 1)
			toggle_burstfire[profile_idx] = 0;
	}
		
	if(f_idx == BURSTFIRE_M)
	{
		if(f_val == 1)
			toggle_rapidfire[profile_idx] = 0;
	}
	return f_val;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function check_toggle() {
	if(modName_idx == ANTIRECOIL_M  && toggle_antirecoil[profile_idx])      return TRUE;
	if(modName_idx == RAPIDFIRE_M   && toggle_rapidfire[profile_idx])       return TRUE;
	if(modName_idx == BURSTFIRE_M   && toggle_burstfire[profile_idx])       return TRUE;
	if(modName_idx == AIMASSIST_M   && toggle_aimassist[profile_idx])       return TRUE;
	if(modName_idx == AIM_ABUSE_M   && toggle_aimabuse[profile_idx])        return TRUE;
	return FALSE;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

define MODNAME_TXT = 0;
define VALNAME_TXT = 1;
define INFO_MENU_PS_TXT = 2;
define INFO_EDIT_PS_TXT = 3;
define INFO_MENU_XB_TXT = 4;
define INFO_EDIT_XB_TXT = 5;
define PROFILE_TXT = 6;

function entry_count(f_array_idx) { // by Swizzy

    i = 0;
    if (f_array_idx == MODNAME_TXT) { while (MODNAME[i] != -1) { i++; } }
    else if (f_array_idx == VALNAME_TXT) { while (VALNAME[i] != -1) { i++; } }
    else if (f_array_idx == INFO_MENU_PS_TXT) { while (INFO_MENU_PS[i] != -1) { i++; } }
    else if (f_array_idx == INFO_EDIT_PS_TXT) { while (INFO_EDIT_PS[i] != -1) { i++; } }
    else if (f_array_idx == INFO_MENU_XB_TXT) { while (INFO_MENU_XB[i] != -1) { i++; } }
    else if (f_array_idx == INFO_EDIT_XB_TXT) { while (INFO_EDIT_XB[i] != -1) { i++; } }
    else if (f_array_idx == PROFILE_TXT) { while (PROFILE[i] != -1) { i++; } }
    return i - 1;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function get_string(f_array_idx, f_idx) { 

	if(f_array_idx == MODNAME_TXT)
		return MODNAME[f_idx];
		
	if(f_array_idx == VALNAME_TXT)
		return VALNAME[f_idx];
			
	if(f_array_idx == INFO_MENU_PS_TXT)
		return INFO_MENU_PS[f_idx];
		
	if(f_array_idx == INFO_EDIT_PS_TXT)
		return INFO_EDIT_PS[f_idx];
			
	if(f_array_idx == INFO_MENU_XB_TXT)
		return INFO_MENU_XB[f_idx];
		
	if(f_array_idx == INFO_EDIT_XB_TXT)
		return INFO_EDIT_XB[f_idx];
		
	if(f_array_idx == PROFILE_TXT)
		return PROFILE[f_idx];
		
	return -1;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int stringLength;
function get_string_length(offset) { 
    stringLength = 0;
    do { 
        offset++;
        stringLength++;
    } while (duint8(offset));
    return stringLength;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 


int c,c_val;

function number_to_string(f_val,f_digits) {

    i = 1;  c_val = 10000;
    
	if(f_val < 0) //--neg numbers
	{          
         putc_oled(i,45);    //--add leading "-"
         i += 1;
         f_val = abs(f_val);
	} 
	
	for(c = 5; c >= 1; c--) 
	{
	    if(f_digits >= c) 
	    {
            putc_oled(i,ASCII_NUM[f_val / c_val]);
            f_val = f_val % c_val;
            i +=  1; 
            /*if(c == 4) 
            {
                putc_oled(i,44);   //--add ","
                i += 1;
            }*/
        }
        c_val /= 10;
    }
    if(display_title)
    	puts_oled(120, 54, OLED_FONT_SMALL, i - 1, OLED_WHITE); // display current # slot
    else
    	puts_oled(center_x(i - 1,OLED_FONT_MEDIUM_WIDTH),28,OLED_FONT_MEDIUM,i - 1,OLED_WHITE); // adjustable value centered in X - medium size
}  

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_base_menu() {
	rect_oled(0, 0, 128, 49, OLED_BLACK, OLED_WHITE); // white contour 
	line_oled(1, 16, 126, 16, 1, OLED_WHITE); // draw line 1
	
// Filled rectangle white (emplacement for the chars line 1)	
	rect_oled(2, 2, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE , OLED_WHITE); // rectangle left (line 1)
	rect_oled(115, 2, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE, OLED_WHITE); // rectangle right (line 1)
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_modMenu() {
// Clears text emplacement line 2 and 3 (value zone while in ModEdit)
	rect_oled(1, 17, 126, 31, OLED_WHITE, OLED_BLACK);
	
// Separate ex value zone
	line_oled(1, 32, 126, 32, 1, OLED_WHITE); // line 2 
	
// Filled rectangle white (emplacement for the chars line 2, 3)
	rect_oled(2, 18, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE , OLED_WHITE); // rectangle left (line 2)
	rect_oled(115, 18, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE, OLED_WHITE); // rectangle right (line 2)
	rect_oled(2, 34, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE , OLED_WHITE); // rectangle left (line 3)
	rect_oled(115, 34, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 3, OLED_WHITE, OLED_WHITE); // rectangle right (line 3)
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_modEdit() {
// Clears line 2 and 3
	rect_oled(1, 17, 126, 31, OLED_WHITE, OLED_BLACK);
	
// Filled rectangle white 
	// Emplacement for the chars line 2, 3)	
	rect_oled(2, 18, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 4, OLED_WHITE , OLED_WHITE); // rectangle left (line 2)
	rect_oled(115, 18, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 4, OLED_WHITE, OLED_WHITE); // rectangle right (line 2)
	rect_oled(2, 33, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 4, OLED_WHITE , OLED_WHITE); // rectangle left (line 3)
	rect_oled(115, 33, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 4, OLED_WHITE, OLED_WHITE); // rectangle right (line 3)
	// Emplacement for + / -
	rect_oled(14, 18, OLED_FONT_MEDIUM_WIDTH + 1, OLED_FONT_MEDIUM_HEIGHT + 11, OLED_WHITE , OLED_WHITE); // rectangle left (line 2 + 3)
	rect_oled(102, 18, OLED_FONT_MEDIUM_WIDTH + 1, OLED_FONT_MEDIUM_HEIGHT + 11, OLED_WHITE, OLED_WHITE); // rectangle right (line 2 + 3)

// Display - / +
	line_oled(16, 31, 23, 31, 2, OLED_BLACK); // left -
	putc_oled(1, 43); // +
	puts_oled(103, 24, OLED_FONT_MEDIUM, 1, OLED_BLACK); // right + 	    

}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

define UP = 131; define DOWN = 132; define LEFT = 133; define RIGHT = 134; define TRIANGLE = 130; define Y = 89; define CROSS = 127; define A = 65;
define LEFT_X = 4; define RIGHT_X = 117;
define LINE_1_Y = 4; define LINE_2_Y = 20;  define LINE_3_Y = 36; define LINE_4_Y = 53;  
int btn_left;
int btn_right;

function draw_chars_line1() {
// Define button
	if(ModMenu)
	{		
		if(get_controller() == PIO_PS4)
		{
			btn_left = TRIANGLE;
			btn_right = TRIANGLE;
		}
		else
		{
			btn_left = Y;
			btn_right = Y;
		}
	}
	if(ModEdit)
	{
		btn_left = LEFT;
		btn_right = RIGHT;
	}

// Clears/fill rectangle
	rect_oled(3, 3, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE , col_rec_left); // rectangle left (line 1)
	rect_oled(116, 3, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE, col_rec_right); // rectangle right (line 1)
// Display chars	
	putc_oled(1,btn_left); 
	puts_oled(LEFT_X, LINE_1_Y, OLED_FONT_SMALL, 1, col_char_left); 	
	putc_oled(1,btn_right); 
	puts_oled(RIGHT_X, LINE_1_Y, OLED_FONT_SMALL, 1, col_char_right); 

}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_chars_line2() {
// Define button
	if (ModMenu || ModEdit)
	{
		btn_left = LEFT;
		btn_right = RIGHT;
	}
	if(ModEdit)
	{
	// Clears/fill rectangle
		rect_oled(3, 19, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 2, OLED_WHITE , col_rec_left); // rectangle left (line 2)
		rect_oled(116, 19, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 2, OLED_WHITE, col_rec_right); //  rectangle right (line 2)
	}
	else
	{
	// Clears/fill rectangle
		rect_oled(3, 19, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE , col_rec_left); //  rectangle left (line 2)
		rect_oled(116, 19, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE, col_rec_right); //  rectangle right (line 2)
	}
// Display chars	
	putc_oled(1,btn_left); 
	puts_oled(LEFT_X, LINE_2_Y, OLED_FONT_SMALL, 1, col_char_left); 	
	putc_oled(1,btn_right); 
	puts_oled(RIGHT_X, LINE_2_Y, OLED_FONT_SMALL, 1, col_char_right); 
	
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_chars_line3() { 
// Define button
	if (ModMenu)
	{
		btn_left = DOWN;
		btn_right = UP;
	}
	if(ModEdit)
	{
		btn_left = DOWN;
		btn_right = UP;
		
	// Clears/fill rectangle
		rect_oled(3, 34, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 2, OLED_WHITE , col_rec_left); // rectangle left (line 3)
		rect_oled(116, 34, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 2, OLED_WHITE, col_rec_right); // rectangle right (line 3)	
	}
	else
	{
	// Clears/fill rectangle
		rect_oled(3, 35, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE , col_rec_left); // rectangle left (line 3)
		rect_oled(116, 35, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT + 1, OLED_WHITE, col_rec_right); // rectangle right (line 3)
	}
// Display chars	
	putc_oled(1,btn_left); 
	puts_oled(LEFT_X, LINE_3_Y, OLED_FONT_SMALL, 1, col_char_left); 
	putc_oled(1,btn_right); 
	puts_oled(RIGHT_X, LINE_3_Y, OLED_FONT_SMALL, 1, col_char_right);  
	
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_hold_to_edit() { 
// Clears/fill the inside of the "hold button" rectangle
	rect_oled(28, 19, OLED_FONT_SMALL_WIDTH + 1 , OLED_FONT_SMALL_HEIGHT , OLED_WHITE , col_rec_left); 
	rect_oled(91, 19, OLED_FONT_SMALL_WIDTH + 2, OLED_FONT_SMALL_HEIGHT  , OLED_WHITE, col_rec_right); 
// Display chars	
	if(get_controller() == PIO_PS4)
	{	
		putc_oled(1, CROSS);
		puts_oled(92, LINE_2_Y - 1, OLED_FONT_SMALL, 1, col_char_left);
		putc_oled(1, CROSS);
		puts_oled(28, LINE_2_Y - 1, OLED_FONT_SMALL, 1, col_char_right);
	}
	else
	{
		putc_oled(1, A);
		puts_oled(92, LINE_2_Y , OLED_FONT_SMALL, 1, col_char_left);
		putc_oled(1, A);
		puts_oled(29, LINE_2_Y , OLED_FONT_SMALL, 1, col_char_right);
	}
// Contour for chars X / A (must be after display chars)
	rect_oled(27, 18, OLED_FONT_SMALL_WIDTH + 3 , OLED_FONT_SMALL_HEIGHT + 2 , OLED_BLACK , OLED_WHITE); // small rectangle left (line 2)
	rect_oled(90, 18, OLED_FONT_SMALL_WIDTH + 4, OLED_FONT_SMALL_HEIGHT + 2 , OLED_BLACK, OLED_WHITE); // small rectangle right (line 2)
}	

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int col_char_left; 
int col_char_right;
int col_rec_left = 1;
int col_rec_right = 1;

function button_animation() {
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	if(event_press(PS4_TRIANGLE))
	{
		col_char_left = 1;
		col_char_right = 1;
		col_rec_left = 0;
		col_rec_right = 0;
		
		if(ModMenu)
			draw_chars_line1();
	}
	else if(event_release(PS4_TRIANGLE))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
			
		if(ModMenu)
			draw_chars_line1();
	}	
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

	if(event_press(PS4_LEFT))
	{
		col_char_left = 1;
		col_char_right = 0;
		col_rec_left = 0;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line2();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line2();
			else
				draw_chars_line1();
		}
	}
	if(event_release(PS4_LEFT))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line2();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line2();
			else
				draw_chars_line1();
		}
	}
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	if(event_press(PS4_RIGHT))
	{
		col_char_left = 0;
		col_char_right = 1;
		col_rec_left = 1;
		col_rec_right = 0;	
		
		if(ModMenu)
			draw_chars_line2();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line2();
			else
				draw_chars_line1();
		}
	}
	if(event_release(PS4_RIGHT))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line2();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line2();
			else
				draw_chars_line1();
		}
	}
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	if(event_press(PS4_UP))
	{
		col_char_left = 0;
		col_char_right = 1;
		col_rec_left = 1;
		col_rec_right = 0;
		
		if(ModMenu)
			draw_chars_line3();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line3();
		}
	}
	if(event_release(PS4_UP))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line3();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line3();
		}
	}
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	if(event_press(PS4_DOWN))
	{
		col_char_left = 1;
		col_char_right = 0;
		col_rec_left = 0;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line3();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line3();
		}
	}
	if(event_release(PS4_DOWN))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
		
		if(ModMenu)
			draw_chars_line3();
			
		if(ModEdit)
		{
			if(get_val(PS4_CROSS))
				draw_chars_line3();
		}
	}
//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	if(event_press(PS4_CROSS))
	{
		col_char_left = 1;
		col_char_right = 1;
		col_rec_left = 0;
		col_rec_right = 0;
		
		if(ModEdit)	
			draw_hold_to_edit();
	}
	if(event_release(PS4_CROSS))//~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
	{
		col_char_left = 0;
		col_char_right = 0;
		col_rec_left = 1;
		col_rec_right = 1;
		
		if(ModEdit)	
			draw_hold_to_edit();
	}
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int SSMU1 = 1,SSMU2 = 8,SSMU3 = 15,SSMU4 = 22,SSMU5 = 29,SSMU6 = 36,SSMU7 = 43;

function cycle(f_val, f_lo, f_hi) {
	if(f_val > f_hi) return f_lo;
	if(f_val < f_lo) return f_hi;
	return f_val;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int X_1,Y_1,X_2,Y_2,X_3,Y_3, X_4,Y_4,X_5,Y_5,X_6,Y_6,X_7,Y_7,X_8,Y_8,RN1,RN2,RN3,RN4,RN5,RN6,RN7,RN8;

function find_digits(f_num) {

    f_num = abs(f_num);
    if(f_num / 10000 > 0) return 5;
    if(f_num /  1000 > 0) return 4;
    if(f_num /   100 > 0) return 3;
    if(f_num /    10 > 0) return 2;
                          return 1;
}     

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function center_x(f_chars,f_font) {

    return (OLED_WIDTH / 2) - ((f_chars * f_font) / 2);
} 

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function center_y(f_size) {

    return (OLED_HEIGHT  / 2) - (f_size / 2);
} 

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

int RT,MSC,SD1,DGT,DGR,TP1,TP2;

function press_hold(f_btn) { 

	return event_press(f_btn) || get_val(f_btn) && get_ptime(f_btn) > 250 
	       && get_ptime(f_btn) % (get_rtime() * 8) == 0;
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function led(f_color) {

   for(i = 0; i <= 3; i++) 
   {
     set_led(i,duint8((f_color * 4) + i));
   }
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜

function name(){
	cls_oled(0);
	combo_run(RACE);
	combo_run(RN);
if (Y_1 > 64)  Y_1 =   0;
if (Y_2 > 64)  Y_2 =   0;
if (Y_3 > 64)  Y_3 =   0;
if (Y_4 > 64)  Y_4 =   0;
if (Y_5 > 64)  Y_5 =   0;
if (Y_6 > 64)  Y_6 =   0;
if (Y_7 > 64)  Y_7 =   0;
if (Y_8 > 64)  Y_8 =   0;
if (SD1 > 105) SD1 = -50;
if (TP1 > 45)  TP1 =   0;
if (TP2 < 0)   TP2 =  55;
	putc_oled(1,RN1);
	puts_oled(X_1,Y_1,0,1,1);
	putc_oled(1,RN2);
	puts_oled(X_2,Y_2,0,1,1);
	putc_oled(1,RN3);
	puts_oled(X_3,Y_3,0,1,1);
	putc_oled(1,RN4);
	puts_oled(X_4,Y_4,0,1,1);
	putc_oled(1,RN5);
	puts_oled(X_5,Y_5,0,1,1);
	putc_oled(1,RN6);
	puts_oled(X_6,Y_6,0,1,1);
	putc_oled(1,RN7);
	puts_oled(X_7,Y_7,0,1,1);
	putc_oled(1,RN8);
	puts_oled(X_8,Y_8,0,1,1);
	putc_oled(1,SSA1);
	puts_oled(SSMU1 + SD1,TP1 + 2,0,1,1);
	putc_oled(1,SSA2);
	puts_oled(SSMU2 + SD1,TP2 + 4,0,1,1);
	putc_oled(1,SSA2);
	puts_oled(SSMU3 + SD1,TP1 + 6,0,1,1);
	putc_oled(1,SSA3);
	puts_oled(SSMU4 + SD1,TP2 + 8,0,1,1);
	putc_oled(1,SSA4);
	puts_oled(SSMU5 + SD1,TP1 + 10,0,1,1);
	putc_oled(1,SSA5);
	puts_oled(SSMU6 + SD1,TP2 + 12,0,1,1);
	putc_oled(1,SSA6);
	puts_oled(SSMU7 + SD1,TP1 + 14,0,1,1);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function block_btn() {

set_val(PS4_UP     , 0 ); 	set_val(PS4_DOWN  , 0 ); 	set_val(PS4_LEFT  , 0 );	set_val(PS4_RIGHT   , 0);
set_val(PS4_CROSS  , 0 ); 	set_val(PS4_CIRCLE, 0 ); 	set_val(PS4_SQUARE, 0 ); 	set_val(PS4_TRIANGLE, 0);
set_val(PS4_L1     , 0 ); 	set_val(PS4_R1    , 0 ); 	set_val(PS4_L3    , 0 ); 	set_val(PS4_R3      , 0);
set_val(PS4_OPTIONS, 0 ); 	set_val(PS4_SHARE , 0 );	set_val(PS4_TOUCH , 0 );	set_val(PS4_PS      , 0);
set_val(XB1_PL1    , 0 ); 	set_val(XB1_PL2   , 0 );	set_val(XB1_PR1   , 0 );	set_val(XB1_PR2     , 0);
/*set_val(PS4_LX, 0); 		set_val(PS4_LY, 0); 		set_val(PS4_RX, 0); 		set_val(PS4_RY, 0);
set_val(PS4_L2, 0); 		set_val(PS4_R2, 0);*/
}  

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 

function draw_logo() {
	print(center_x(sizeof(TITLE1) - 1,OLED_FONT_LARGE_WIDTH),1,OLED_FONT_LARGE,OLED_WHITE,TITLE1[0]);
	print(center_x(sizeof(TITLE2) - 1,OLED_FONT_LARGE_WIDTH),22,OLED_FONT_LARGE,OLED_WHITE,TITLE2[0]);
}

// 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜 〜


function save() {
	ModMenu = FALSE;
	ModEdit = FALSE;
	cls_oled(OLED_BLACK);
	print(center_x(sizeof(SAVE) - 1,OLED_FONT_LARGE_WIDTH),center_y(OLED_FONT_LARGE_HEIGHT),OLED_FONT_LARGE,OLED_WHITE,SAVE[0]); 	// display SAVED... centered in X Y 
	combo_run(SAVE);
	profile_idx = last_profile;
	
// Profile 1
	// Toggles                                      // Values
	set_pvar(SPVAR_1,toggle_antirecoil[0]);         set_pvar(SPVAR_5 ,antirecoil_vertical[0]  );
	set_pvar(SPVAR_2,toggle_rapidfire[0] );         set_pvar(SPVAR_8 ,antirecoil_horizontal[0]);
	set_pvar(SPVAR_3,toggle_burstfire[0] );         set_pvar(SPVAR_9,rounds_per_second[0]          );
	set_pvar(SPVAR_4,toggle_aimassist[0] );         
	set_pvar(SPVAR_6,toggle_aimabuse[0]  );         
	set_pvar(SPVAR_35,toggle_headshot[0] );         
// Profile 2
	// Toggles                                       // Values
	set_pvar(SPVAR_10,toggle_antirecoil[1]);         set_pvar(SPVAR_14,antirecoil_vertical[1]  );
	set_pvar(SPVAR_11,toggle_rapidfire[1] );         set_pvar(SPVAR_17,antirecoil_horizontal[1]);
	set_pvar(SPVAR_12,toggle_burstfire[1] );         set_pvar(SPVAR_18,rounds_per_second[1]         );
	set_pvar(SPVAR_13,toggle_aimassist[1] );        
	set_pvar(SPVAR_15, toggle_aimabuse[1] );         
	set_pvar(SPVAR_36,toggle_headshot[1]  );         
// Profile 3
	// Toggles                                       // Values
	set_pvar(SPVAR_19,toggle_antirecoil[2]);         set_pvar(SPVAR_23,antirecoil_vertical[2]  );
	set_pvar(SPVAR_20,toggle_rapidfire[2] );         set_pvar(SPVAR_26,antirecoil_horizontal[2]);
	set_pvar(SPVAR_21,toggle_burstfire[2] );         set_pvar(SPVAR_27,rounds_per_second[2]         );
	set_pvar(SPVAR_22,toggle_aimassist[2] );         
	set_pvar(SPVAR_24,toggle_aimabuse[2] );        
	set_pvar(SPVAR_37,toggle_headshot[2]  );         

// Misc Toggles
	set_pvar(SPVAR_28, autorun_on );
	set_pvar(SPVAR_29, rumble_on  );
	set_pvar(SPVAR_34, ceo_on  );
	
// Misc Values
	set_pvar(SPVAR_30, burstfire_hold   );
	set_pvar(SPVAR_31, burstfire_release);
	set_pvar(SPVAR_32, aimassist_radius );
	set_pvar(SPVAR_33, aimassist_angle  );
	set_pvar(SPVAR_38, aimabuse_hold    );
	set_pvar(SPVAR_39, aimabuse_release );
} 