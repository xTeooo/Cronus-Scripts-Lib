// GPC Online Library
// elder_scrolls_online_(eso)_gpc_script.gpc

/////////////////////////////////////////////
//  Elder Scrolls Online (ESO) GPC Script  //
//   Updated by Jebstream on 2018-06-02    //
/////////////////////////////////////////////

/////////////////////////
// Section 1: Controls //
/////////////////////////
/*

Reset Controls
	1. Press Start to disable scripts
		a. If you hold LS/L3 no scripts will be disabled

Weaving Controls
	1. Hold D-Pad Down and RS/R3 >= 2 seconds to enable script (has rumble notification)
	2. Press a skill button to run weaving combo which light attacks, then a skill, and block cancels
		b. Hold LS/L3 and press LB/L1 + RB/R1 to cast an Ultimate skill

Rotation Controls
	1. Hold LS|L3 >= 1 second and press X|Square to enable script (has rumble notification)

Fishing Controls
	1. First select your bait for the appropriate fishing spot
	1. Hold D-Pad Up and RS/R3 >= 2 seconds to enable script (has rumble notification)
	2. Look at the fishing spot and press A|Cross

Lockpicking Controls
	1. Press A|Cross to open the lock picking screen.
	2. Hold D-Pad Left and RS/R3 >= 2 seconds to enable script

*/
/////////////////////////
// Section 3: Settings //
/////////////////////////

int input_ready = TRUE;
int weaving_state = FALSE;
int weaving_counter = 0;
int fishing_state = FALSE;
int fishing_timer = 0;
int lockpicking_state = FALSE;
int picking_lock = FALSE;
int tumblers_remaining = 0;
int tumblers_rumbling = 6;	// Should be 6 for XB1 or 75 for PS4
int button_pressed = FALSE;

////////////////////////
// Section 4: Scripts //
////////////////////////

main
{

	// Disable Scripts (Menu|Start)
	if(event_press(2) && !get_val(8)){
		combo_run(disable);
		if(weaving_state || fishing_state || lockpicking_state){combo_run(rumble);}
	}

	// Enable weaving combo (D-Pad Down >= 2 seconds && RS|R3)
	if(input_ready && get_ptime(14) >= 2000 && get_val(14) == 100 && get_val(5)){
		input_ready = FALSE;
		weaving_state = TRUE;
		combo_run(rumble);
	}

	// Weaving script
	if(weaving_state && !get_val(8)){
		if(event_press(20) || event_press(17) || event_press(18) || event_press(6) || event_press(3)){    
			weaving_counter = 0;
			if(get_val(20) && weaving_state){weaving_counter = 1;}
			else if(get_val(17) && weaving_state){weaving_counter = 2;}
			else if(get_val(18) && weaving_state){weaving_counter = 3;}
			else if(get_val(6) && weaving_state){weaving_counter = 4;}
			else if(get_val(3) && weaving_state){weaving_counter = 5;}
			if(weaving_counter > 0){combo_run(weaving);}
		}
	}

	// Enable rotation combo (LS|L3 >= 1 seconds && X|Square)
	if(input_ready && get_ptime(8) >= 1000 && get_val(8) == 100 && get_val(20)){
		input_ready = FALSE;
		combo_run(rotation);
		combo_run(rumble);
	}

	// Enable fishing script (D-Pad Up >= 2 seconds + RS/R3)
	if(input_ready && get_ptime(13) >= 2000 && get_val(13) == 100 && get_val(5)){
		input_ready = FALSE;
		fishing_state = TRUE;
		combo_run(rumble);
	}

	// Fishing script
	if(fishing_state && get_rumble(RUMBLE_B) > 0){fishing_timer = fishing_timer + get_rtime();}
	else if(fishing_state && !get_rumble(RUMBLE_B) <= 0){
		fishing_timer = 0;}
	if(fishing_state && fishing_timer >= 1500){
		combo_run(fishing);
	}

	// Enable lockpicking script (D-Pad Left >= 2 seconds + RS/R3)
	if(input_ready && get_ptime(15) >= 2000 && get_val(15) == 100 && get_val(5)){
		input_ready = FALSE;
		lockpicking_state = TRUE;
	}

	// Lockpicking script
	if(input_ready && lockpicking_state){
		lockpicking_state = FALSE;
		combo_run(open_chest);
	}
	if(input_ready && picking_lock){rt();}
	if(input_ready && picking_lock && get_rumble(RUMBLE_B) > tumblers_rumbling && !button_pressed){
		button_pressed = TRUE;
		combo_run(pick_lock);
	}

}

///////////////////////
// Section 5: Combos //
///////////////////////

combo weaving // Light weaving, skill attack, and block cancelling
{
	rt();wait(100);wait(100);
    if(weaving_counter == 1){x();}
    else if(weaving_counter == 2){y();}
    else if(weaving_counter == 3){b();}
    else if(weaving_counter == 4){lb();}
    else if(weaving_counter == 5){rb();}
    wait(100);wait(100);
    lt();wait(100);wait(100);
}

combo rotation // Rotation with weaving and cancelling
{
    rt();wait(100);wait(100);		// Light Attack
    x();wait(100);wait(100);		// X|Sqaure
	lt();wait(100);wait(600);		// Block Cancel
	rt();wait(100);wait(100);		// Light Attack
    y();wait(100);wait(100);		// Y|Triangle
	lt();wait(100);wait(600);		// Block Cancel
	rt();wait(100);wait(100);		// Light Attack
    b();wait(100);wait(100);		// B|Circle
	lt();wait(100);wait(600);		// Block Cancel
	rt();wait(100);wait(100);		// Light Attack
    lb();wait(100);wait(100);		// LB|L1
	lt();wait(100);wait(600);		// Block Cancel
	rt();wait(100);wait(100);		// Light Attack
    rb();wait(100);wait(100);		// RB|R1
	lt();wait(100);wait(400);		// Block Cancel
	l();wait(100);wait(100);		// Bar Swap
}

combo fishing
{
    a();wait(100);wait(2600);
    a();wait(100);wait(100);  
    fishing_timer = 0;
}

combo open_chest
{
    insert();wait(400);wait(50);
    tumblers_remaining = 5;
    picking_lock = TRUE;
}
 
combo pick_lock
{
    scrub();
    wait(200);
    torque();
    tumblers_remaining = tumblers_remaining - 1;
    button_pressed = FALSE;
}

combo rumble
{
    set_rumble(RUMBLE_A, 20);		wait(150);
    reset_rumble();					wait(200);
    set_rumble(RUMBLE_A, 20);		wait(150);
    reset_rumble();					wait(200);
    set_rumble(RUMBLE_A, 20);		wait(150);
    reset_rumble();					wait(1150);
    input_ready = TRUE;
}

combo disable
{
    wait(100);wait(100);
    input_ready = TRUE;
    weaving_state = FALSE;
    fishing_state = FALSE;
    lockpicking_state = FALSE;
    picking_lock = FALSE;
    button_pressed = FALSE;
}

//////////////////////////
// Section 5: Functions //
//////////////////////////

function home(){set_val( 0, 100);}					// Home
function start(){set_val( 2, 100);}					// Menu|Start
function select(){set_val( 1, 100);}				// View|Select
function a(){set_val(19, 100);}						// A|Cross
function x(){set_val(20, 100);}						// X|Square
function y(){set_val(17, 100);}						// Y|Triangle
function b(){set_val(18, 100);}						// B|Circle
function rb(){set_val(3, 100);}						// RB|R1
function rt(){set_val(4, 100);}						// LT|L2
function rs(){set_val(5, 100);}						// RS|R3
function lb(){set_val(6, 100);}						// LB|L1
function lt(){set_val(7, 100);}						// LT|L2
function ls(){set_val(8, 100);}						// LS|L3
function ult(){set_val(3, 100);set_val(6, 100);}	// RB|R1 && LB|L1
function u(){set_val(13, 100);}						// D-Pad Up
function d(){set_val(14, 100);}						// D-Pad Down
function l(){set_val(15, 100);}						// D-Pad Left
function r(){set_val(16, 100);}						// D-Pad Right
function insert(){set_val(11, -100);}
function scrub(){set_val(4, 0);if(tumblers_remaining > 1){set_val(11, 100);}}
function torque(){set_val(11, 0);if(tumblers_remaining < 2){set_val(4, 0);picking_lock = FALSE;}}