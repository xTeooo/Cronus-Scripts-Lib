// GPC Online Library
// rust_console_ak_.gpc


																																												/*
 .~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~.
(                    JB'S Rust AK Anti Recoil        )
 `-~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-*

This is not any scammers crap combo script that does not work at all. You can configure this to any gun any setting any attachment. The timing will need to be changed per gun.

This script allows you to have anti recoil with aiming. Each bullet has a value which can be changed. 

The values are pretty decent at this point. You can fine tune them if need be.

Currently I have it set up for an AK with a holo.

READ ALL THE INSTRUCTIONS BELOW IN GREEN TEXT.

--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
 .~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~.
(              In Game Values                        )
 `-~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-*
 
 70 - X
 70 - Y
 40 - ADS
 40 - 1 X Zoom
 10 - Dead Zone Left
 10 - Dead Zone Right
 90 - FOV under Graphics. Not sure if this has an effect or not.

																																												*/   



define Ads      = PS4_L2; // DO NOT TOUCH
define Fire     = PS4_R2; // DO NOT TOUCH


																																												/*
--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

 .~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~.
(                    Script Stick Drift              )
 `-~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-*

1)	DO NOT SET YOUR IN GAME VALUE TO THIS. 
2)	THIS IS VERY IMPORTANT!!! This allows you to remove stick drift from the script. Move your right stick around and find the highest value in device manager.  
3)	See what your MAX INPUT is for your left stick X and Y when you dont touch it.
4)	I recomend moving it around a little bit and see where it falls.
6)	Mine was 7. It does not matter if it is positive or negative. Thats why 7 is in the dead zone above
7)  Your stick drift cannot exceed a value for the anti recoil below.
8)  LEAVE YOUR IN GAME DEADZONE AT 10 DO NOT SET IT TO THE ONE BELOW
					 																																							*/

define Deadzone = 7; 

																																												/* 
-------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

 .~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~.
(                    Anti Recoil Values              )
 `-~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~-*

1)	Values below are for the recoil pattern. You can adjust each bullet. The values are pretty solid. You can fine tune them if you want.
2)  I recommend changing values only by increasing or decreasing the value by 1 at a time. Max 2 at a time.
3)  X is Horzontal
4)  Y is Vertical 
																																												*/
const int16 AK[][] = {
	{0,-20,-10,-25,-18,  8, 12, 32,  23,  23,  23,  24,  23,   4, -13, -13, -13, -20, -21, -26, -26, -26, -26,  18,  18,  20,  20,  20,  20,  20,   0},			//X-Axis
	{0, 49, 49, 49, 49, 46, 27, 27,  27,  27,  28,  28,  28,  41,  41,  41,  41,  41,  41,  36,  36,  36,  36,  36,  34,  34,  34,  34,  30,  30,   0},			//Y-Axis
	{0,130,260,390,520,650,780,910,1040,1170,1300,1430,1560,1690,1820,1950,2080,2210,2340,2470,2600,2730,2860,2990,3120,3250,3380,3510,3640,3770,4000}  		//Time
}

int X_Strength_modifier	//Boost X Axis Strnegth
int Y_Strength_modifier	//Boost Y Axis Strnegth

int AntiRecoil_Y;     // DO NOT TOUCH
int AntiRecoil_X;     // DO NOT TOUCH
int AntiRecoil_Y_Use; // DO NOT TOUCH
int AntiRecoil_X_Use; // DO NOT TOUCH

int recoil_timer,wtime,vpower,hpower,time_array,step;

init
{
    X_Strength_modifier = get_pvar(SPVAR_1, -99, 200, 0);
    Y_Strength_modifier = get_pvar(SPVAR_2, -99, 200, 0);
}

//Menu Instructions

//Press L2/LT + Options/Menu to enter menu
/*In the main page of menu, 
Press CROSS/A to enter value adjustment page,*/

/*In the value adjustment page, 
You can increase/decrease values by 10 by holding L2/LT and pressing up/down, or only change values by 1 by only pressing up/down,
Press L1/LB to adjust x values, press R1/RB to adjust y values*/

//You can go back to the last page of the menu by pressing Circle/B
//Press Triangle/Y in any page of the menu to exit menu
//Press L2/LT + Triangle/Y inside or outside the menu to save values

//Some other instructions are inside the menu

const uint8 Buttons [] = {
 PS4_L2,PS4_OPTIONS,PS4_LEFT, PS4_RIGHT,PS4_UP,PS4_DOWN,PS4_CROSS,PS4_CIRCLE,PS4_SQUARE,PS4_TRIANGLE,PS4_L1,PS4_R1,PS4_R2};
int Menu; 
int ValuePage;
int XValuePage = TRUE;
int YValuePage = FALSE;
int MainPage;
int HelpPage1;
int HelpPage2;
int Title = TRUE;
int blockButton;
int i;
int i_val, c_val, chr_val; 

const string TitleName = "AK Legit";   //You can change AK to whatever title you want
const string TitleMessage []= {"Press L2/LT +", "Options/Menu"};
const string MainPageMessage1 [] = {"Press Cross/A", "to continue"};
const string MainPageMessage2 [] = {"Press Triangle/Y", "to exit"};
const string ValuePageMessage [] = {"Press Square/X", "for help"};
const string HelpPage1Message [] = {"Press L1/R1 to", "adjust x/y", "Cross/A:Next Page", "Circle/B:Last Page"};
const string HelpPage2Message [] = {"Hold L2 and press", "Up/Down:", "Change value by 10", "Press Up/Down:", "Change value by 1"};
const string XValue = "X Strength";
const string YValue = "Y Strength";

  																																												/*
 DO NOT CHANGE ANYTHING BELOW. Unless you are configuring for a different weapon. You will need to change the value after get_ptime.
------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------
																																												*/



main {

    if(get_val(PS4_L2) && event_press(PS4_TRIANGLE))
    {
        SaveValues();
    }
    if(Title)
    {
        cls_oled(OLED_BLACK);
        printf(CenterText(sizeof(TitleName) - 1, OLED_FONT_MEDIUM_WIDTH), 13, OLED_FONT_MEDIUM, OLED_WHITE, TitleName[0]);
        printf(23, 40, OLED_FONT_SMALL, OLED_WHITE, TitleMessage[0]);
        printf(25, 53, OLED_FONT_SMALL, OLED_WHITE, TitleMessage[1]);
    }
    if(MainPage)
    {
        cls_oled(OLED_BLACK);
        printf(20, 0, OLED_FONT_SMALL, OLED_WHITE, MainPageMessage1[0]);
        printf(30, 10, OLED_FONT_SMALL, OLED_WHITE, MainPageMessage1[1]);
        printf(10, 45, OLED_FONT_SMALL, OLED_WHITE, MainPageMessage2[0]);
        printf(43, 55, OLED_FONT_SMALL, OLED_WHITE, MainPageMessage2[1]);
        
        if(event_press(PS4_CROSS))
        {
            ValuePage = TRUE; 
            MainPage = FALSE; 
        }
    }
    if(ValuePage)
    {
        cls_oled(OLED_BLACK);
        printf(15, 40, OLED_FONT_SMALL, OLED_WHITE, ValuePageMessage[0]);
        printf(35, 50, OLED_FONT_SMALL, OLED_WHITE, ValuePageMessage[1]);
        if(event_press(PS4_SQUARE))
        {
            HelpPage1 = TRUE;
            ValuePage = FALSE;
        }
        if(event_press(PS4_CIRCLE))
        {
            ValuePage = FALSE;
            MainPage = TRUE;
            XValuePage = TRUE;
            YValuePage = FALSE;
        }
        if(event_press(PS4_L1))
        {
            XValuePage = TRUE;
            YValuePage = FALSE;
        }
        if(event_press(PS4_R1))
        {
            XValuePage = FALSE;
            YValuePage = TRUE;
        }
                
        if(XValuePage)
        {
            printf(CenterText(sizeof(XValue) - 1, OLED_FONT_MEDIUM_WIDTH), 0, OLED_FONT_MEDIUM, OLED_WHITE, XValue[0]);
            NumberToString(X_Strength_modifier, FindDigits(X_Strength_modifier), CenterText(FindDigits(X_Strength_modifier), OLED_FONT_MEDIUM_WIDTH), 20);	
            if(event_press(PS4_UP))
            {
                if(get_ival(PS4_L2))
                {
                    X_Strength_modifier += 10;
                }
                else { X_Strength_modifier ++;
                }
            }
            if(event_press(PS4_DOWN))
            {
                if(get_ival(PS4_L2))
                {
                    X_Strength_modifier -= 10;
                }
                else { X_Strength_modifier --;
                }
            }
            if(X_Strength_modifier > 200) X_Strength_modifier = 200;
            if(X_Strength_modifier < -99) X_Strength_modifier = -99;
        }
        if(YValuePage)
        {
            printf(CenterText(sizeof(YValue) - 1, OLED_FONT_MEDIUM_WIDTH), 0, OLED_FONT_MEDIUM, OLED_WHITE, YValue[0]);
            NumberToString(Y_Strength_modifier, FindDigits(Y_Strength_modifier), CenterText(FindDigits(Y_Strength_modifier), OLED_FONT_MEDIUM_WIDTH), 20);	
            if(event_press(PS4_UP))
            {
                if(get_ival(PS4_L2))
                {
                   Y_Strength_modifier += 10;
                }
                else { Y_Strength_modifier ++;
                }
            }
            if(event_press(PS4_DOWN))
            {
                if(get_ival(PS4_L2))
                {
                    Y_Strength_modifier -= 10;
                }
                else { Y_Strength_modifier --;
                }
            }
            if(Y_Strength_modifier > 200) Y_Strength_modifier = 200;
            if(Y_Strength_modifier < -99) Y_Strength_modifier = -99;
        }
    }
    
    if(HelpPage1)
    {
        cls_oled(OLED_BLACK);
        printf(0, 0, OLED_FONT_SMALL, OLED_WHITE, HelpPage1Message[0]);
        printf(0, 10, OLED_FONT_SMALL, OLED_WHITE, HelpPage1Message[1]);
        printf(0, 40, OLED_FONT_SMALL, OLED_WHITE, HelpPage1Message[2]);
        printf(0, 50, OLED_FONT_SMALL, OLED_WHITE, HelpPage1Message[3]);
        if(event_press(PS4_CIRCLE))
        {
            HelpPage1 = FALSE;
            ValuePage = TRUE;
        }
        if(event_press(PS4_CROSS))
        {
            HelpPage2 = TRUE;
            HelpPage1 = FALSE;
        }
    }
    
    if(HelpPage2)
    {
        cls_oled(OLED_BLACK);
        printf(0, 0, OLED_FONT_SMALL, OLED_WHITE, HelpPage2Message[0]);
        printf(0, 10, OLED_FONT_SMALL, OLED_WHITE, HelpPage2Message[1]);
        printf(0, 20, OLED_FONT_SMALL, OLED_WHITE, HelpPage2Message[2]);
        printf(0, 30, OLED_FONT_SMALL, OLED_WHITE, HelpPage2Message[3]);
        printf(0, 40, OLED_FONT_SMALL, OLED_WHITE, HelpPage2Message[4]);
        printf(0, 50, OLED_FONT_SMALL, OLED_WHITE, HelpPage1Message[3]);
        if(event_press(PS4_CIRCLE))
        {
            HelpPage2 = FALSE;
            HelpPage1 = TRUE;
        }
    }
        
    if(blockButton) 
    {
		if(event_release(blockButton)) 
		{
			blockButton = 0;
		}
		else
		{
	    	set_val(blockButton,0);
	    }
	}
	
    if(get_val(PS4_L2) && get_val(PS4_OPTIONS) && get_ptime(PS4_OPTIONS) > 1000)
	    {
        Menu = TRUE;
        MainPage = TRUE;
        Title = FALSE;
        blockButton = PS4_OPTIONS;
        }
    if(Menu)
    {
        BlockButtonPress();
        if(event_press(PS4_TRIANGLE)) 
        {
            combo_run(EXIT);
        }
    }
 
	set_val(TRACE_1,AntiRecoil_X);
	set_val(TRACE_2,AntiRecoil_Y);
	set_val(TRACE_3,wtime);
	set_val(TRACE_4,step);
	
	AntiRecoil_X = AK [0][hpower];
	AntiRecoil_Y = AK [1][vpower];
	wtime        = AK [2][time_array]; 
	
	AntiRecoil_Y_Use = (AntiRecoil_Y * (100 + Y_Strength_modifier)) / 100;
    AntiRecoil_X_Use = (AntiRecoil_X * (100 + X_Strength_modifier)) / 100;
    
  if(isqrt(pow(abs(get_ival(PS4_LX)),2) + pow(abs(get_ival(PS4_LY)),2)) <= Deadzone) {
    set_val(PS4_LX,0);
    set_val(PS4_LY,0);
  }

  if(isqrt(pow(abs(get_ival(PS4_RX)),2) + pow(abs(get_ival(PS4_RY)),2)) <= Deadzone) {
    set_val(PS4_RX,0);
    set_val(PS4_RY,0);
  }
   
  if(get_val(Fire) && get_val(Ads)){
  	recoil_timer += get_rtime();   
	if(recoil_timer >= wtime){ 
		vpower++;
	    hpower++;
	    time_array++;
	    step++;
	}
	
	set_val(PS4_RY, SmartSens(AntiRecoil_Y_Use, PS4_RY));
    set_val(PS4_RX, SmartSens(AntiRecoil_X_Use, PS4_RX));
    
        
    }
    
	if(event_release(Fire) || AK [2][time_array] == AK [2][30]){ 
		vpower       = 0;
	    hpower       = 0;
	    time_array   = 0;
	    step         = 0;
	    recoil_timer = 0;
	}
    
    
}

combo EXIT 
{
    wait(100);
    cls_oled(OLED_BLACK); 
    Menu = FALSE;
    MainPage = FALSE; 
    ValuePage = FALSE; 
    HelpPage1 = FALSE;
    HelpPage2 = FALSE;
    XValuePage = TRUE;
    YValuePage = FALSE;
    Title = TRUE;
}


int SmartSens;

function SmartSens(f_val, f_axis) { 

    SmartSens = ( (f_val * ( 100 - abs( get_val(f_axis) ) ) ) / 100); 

    return clamp(get_val(f_axis) + SmartSens, -100, 100);
}

function CenterText(f_chars,f_font)
{
    return (OLED_WIDTH / 2) - ((f_chars * f_font) / 2); 
}

function BlockButtonPress ()
{
    for(i = 0; i < 13; i++)
    {
        if(get_ival(Buttons[i]) || event_press(Buttons[i]))
        {
            set_val(Buttons[i],0);
        }
    }
}

function NumberToString(f_val,f_dgts, x_val,y_val) {
 
	i_val = 1;  chr_val = 10000;
    
	if(f_val < 0){          
         putc_oled(i_val,45);    
         i_val += 1;
         f_val = abs(f_val);
	} 
	for(c_val = 5; c_val >= 1; c_val--){
	    if(f_dgts >= c_val) {
            putc_oled(i_val, (f_val / chr_val) + 48);
            f_val = f_val % chr_val;
            i_val +=  1; 
            if(c_val == 4){
                putc_oled(i_val,44);   
                i_val += 1;
            }
        }
        chr_val /= 10;
    }  

    puts_oled(x_val, y_val, 1,i_val - 1, 1);    
} 

function FindDigits(digits) {                         
	//  FindDigits(value)                                
	//        return Number of Digits in Value Passed     		                                                      
    digits = abs(digits);                               
    if(digits / 10000 > 0) return 5;                   
    if(digits /  1000 > 0) return 4;                   
    if(digits /   100 > 0) return 3;                   
    if(digits /    10 > 0) return 2;                   
                          return 1;                  
}

function SaveValues() 
{
    set_pvar(SPVAR_1, X_Strength_modifier);
    set_pvar(SPVAR_2, Y_Strength_modifier);
}
