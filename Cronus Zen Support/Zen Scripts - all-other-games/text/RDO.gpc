                                                                                                                                                                                                                                                                                             /*
              %%%%    %%%%   %%    %%%%%     %%%%%%   %%%     %%%%%%    %%%      %%%                    
               %%%%%   %%%%%  %%  %%%       %%         %%%   %%%    %%%  %%%%%%    %%                   
                %%% %% %% %%   %%   %%%%%%%   %%%%%%%%   %%  %%%      %%% %%% %%%   %%                  
                 %%  %%%%  %%  %%%        %%%        %%% %%%  %%%     %%%  %%    %%%%%%                 
                  %%  %%%  %%%  %%% %%%%%%%%   %%%%%%%%   %%%   %%%%%%%%   %%%      %%%%                
                  %%%       %%%  %%                                         %%%        %%               
                 %%%  %%%  %%%  %%%  %%%%    %%%  %%%%    %%%  %%%%%%%%%%  %%%  %%%  %%%                
                 %%  %%%% %%%  %%%  %%%%%   %%%  %%%%%%  %%% %%%      %%%  %%  %%%% %%%                 
                %%%%%  %% %%  %%%  %%%  %% %%%  %%%  %%  %%  %%%      %%  %%%%%  %% %%                  
               %%%%%   %%%%%  %%  %%%    %%%%%  %%    %%%%   %%%%%%%%%%  %%%%%   %%%%%                  
________________________________________________________________________________________________________









                     GBM#@.BB#@@@    #M   ###@@@  @@  ##  hM@#  #BB#@@@   ##;                           
                    2@@@@@ B@#      ,@#@  @H@@@@  @@  @@ @@#M#@ X@@     @@B@@X                          
                    B@G    @@G:Si   @@ #@   @@H  h@@  @@ @@  #@ #@#,iS  @@#                             
                    @@@@@# @@@     B@5 B#   @@&  @@# ,#@ @@@@@  @@h      @@#@                           
                    B@s    @##     @@@@#Ah  3@   @@@ @@@ @@ @@H @@9         @Hs                         
                    @@s    BBB#@@ @@@  @@@  &@    @@@@@  @@  @@ MAMB@@# A@@@@@                          




	Automatic detection of the weapon slot in the weapon selector that is used.
	Automatic switching of modes depending on the selected slot.
	Skip tonics animation.
	Using a sectorized touchpad to use skip tonics animation.
	Using tonics during dead eye.
	Horse running clicker.
	Clicker for pistols.
	Clicker for repeaters.
	Hip-fire mode with 100 percent accuracy (usualy use for sniper rifles, increases the rate of fire).
	Rapid fire for Carcano using crouch glitch.
	Rapid fire for Carcano using touchpad glitch and horse.
	Rapid fire for bow.
	The script itself determines when you are in the pause menu or inventor menu and don't use hip-fire for sniper rifles and cross clicker.
	Automatic return to previous settings after exiting the menu.
	User interaction via the lightbar when changing sniper rifle and auto headshot modes.
	Auto headshot for long range.
	Auto headshot for middle range.
	Automatic activation of dead eye during auto headshot if not used by user.
	Option to not use auto headshot without switches.
	Slots are configured using code.
	Low battery notification.
	Improved aim assist for sniper rifles.
	Enable / disable improved aim assist for sniper rifles.
	User interaction via vibration.
	Hair trigger.
	Quickly put on a hat from a legendary costume.




________________________________________________________________________________________________________________________________________________________________________________________________________________                                                                                                             




          @ @   @  @@@  @@@@ G@@@#  @   @  H@@  @@@@@ @  ,@@H  @   @    @@@@  .@@H     @                
          @ @@  @ @       @  G    h @   @ @   G   @   @ #    @ @@  @    @   @ @       B@                
          @ @ @ @  B@@    @  G@@@#  @   @ @       @   @ @    @ @ @ @    @MM@.  @@#   M @                
          @ @  @@     @   @  G   @  @   @ @   9   @   @ #    @ @  @@    @         @ @@@@3               
          @ @   @  @@@    @  G    @  @@@   @@@    @   @  X@@#  @   @    @     5@@@     @                





	Color indications
		
		Green color        = The script is ready to use rapid fire for Carcano using crouches glitch now.
		White color        = The script is not ready to use any rapid fire or auto headshot.
		Red color          = The script is ready to use Carcano rapid using touchpad glitch and horse now.
		Yellow color       = The script thinks that you are on the inventory,pause menu or in the catalog now.
		SkyBlue color      = The script is ready to use auto headshot for long distance.
		Pink color         = The script is ready to use auto headshot for middle distance.
		Light bar flashing = Battery charge less than 10 percent.
		
	Rumble indications 
		
		Strong vibration on the left side of the controller = Improved aim assist for sniper rifles has been enabled.
		Small vibration on the right side of the controller = Improved aim assist for sniper rifles has been disabled.
		
	Before using
		
		Settings in the game that you need to change
			
			Invert Look      = Off.
			Vibration        = Off.
			Light Bar Effect = Off.
			Third Person Controls = Standard FPS.
			First Person Controls = Standard FPS.
			
	Your layout now
		
		Aim/Switch item in the selector  - - - - - - - - - = L2;
		Shoot/Switch item in the selector  - - - - - - - - = R2;
		Run  - - - - - - - - - - - - - - - - - - - - - - - = L3;
		Weapon selector  - - - - - - - - - - - - - - - - - = L1;
		Crouch/Run on the horse  - - - - - - - - - - - - - = Cross;
		Jump - - - - - - - - - - - - - - - - - - - - - - - = Square;
		Sit on the horse - - - - - - - - - - - - - - - - - = Triangle;
		Cancel - - - - - - - - - - - - - - - - - - - - - - = Сircle;
		Look behind/Dead eye - - - - - - - - - - - - - - - = R3; 
		Hide behind cover/Switch to tonics in the selector = R1;
		
	Using
		
		Mods that are used for each of the weapon selector slots
			
			Three personal weapon slots that are at the top using clicker and auto headshot.
			The left slot at nine o'clock uses the same.
			The slots at 7 o'clocl,at 5 o'clocl and at 3 o'clock don't use any mode.
			The slot at 6 o'clock activates only sniper mode.
			
			!!! The script determines which slot you use after you hold down L1 button and select a weapon. If this did not get the desired result, then just select this weapon again. !!!
			
		Sniper mode
			
			If you choose the botton slot, aim and press RIGHT on D-pad then you will switch modes for the sniper rifle.
				
				Rapid fire for Carcano using crouches glitch mode.                                       (The LED is Green)
				Rapid fire for Carcano using touch panel glitch and horse, also it's rapid fire for bow. (The LED is Red)
				To disable modes for this slot just aim and hold RIGHT on D-pad.                         (The LED is White)
				
			If any of these modes is active, then the sniper rifle hip-fire mode with 100% accuracy will be enabled even with Slippery Bastart.
			The script itself detects when you are in the inventory,pause menu or in the catalog and turns off the sniper mode and cross clicker. Only works when your lightbar is red or green. (The LED is Yellow)
			If you exited the inventory or pause menu and the color light is still yellow just press the cancel button a couple of times or hold it for a second. 
			Pause mode disables the cross clicker. It helps when you prepare tonics. Just hold RIGHT or LEFT on D-pad when your lightbar is red or green and you prepare tonics.
			You do not need to turn on rapid fire every time. The script remembers this for this slot.
			If during aiming you press the L1 key you enable or disable the improved aim assist for sniper rifles, you will also feel the vibration.
			
		Using modes
			
			Clicker
				
				Just hold the fire button more than 130 milliseconds.
				
			Auto headshot
				
				Work when your LED is White
					
					If after aiming you press L1 button the combo for the long distance will be activated.
					If after aiming you hold L1 button the combo for middle distance will be activated.
					If you don't press/hold L1 at all auto headshot won't be activated. 
					You dont need to press the dead eye activation button while auto headshot. The script will do it for you.
					
				OR
					
					If you choose the left slot or slot with personal weapons, aim and press RIGHT on D-pad then you will switch modes for the auto headshot. 
					Auto headshot turns on automatically when you aim. Depending on the color of LED this works for different distances.
						
						SkyBlue color means it works for long distance.
						Pink color means it works for middle distance.
						To disable modes for this slot just aim and hold RIGHT on D-pad. (The LED is White)
						
					You dont need to press the dead eye activation button while auto headshot. The script will do it for you.
					
			Hip-fire mode
				
				Just press the fire button while not aiming. 
				Don't press this too often as you will see your scope.
				Recommended not to stand still.
				Or just hold down the fire button. Carcano rapid with 100 accuracy will turn on and you will see the flickering of the sight.
				
			Rapid fire for Carcano using crouches glitch mode
				
				Just press the fire button while aiming (press this when you see the crosshair become white, over time you will get used to doing it with sound).
				
			Rapid fire for Carcano using touch panel glitch and horse mode
				
				Sit on your horse and hold the fire button while aiming.
				Or take a bow and you can use rapid fire for bow.
				
		Recommented weapon layout
			
			The Top slot    - Any personal weapons.
			The Left slot   - Repeaters, Rifles, Shotguns.
			The Bottom slot - Sniper Rifles, Bows, Shotguns.
			
		Horse running clicker
			
			Sit on your horse.
			If you hold down the cross button for more than 2 seconds, the script will start the cross clicker.
			
		Skip tonics animation
			
			The touchpad is divided into two parts. Which is above the horizontal line in the center and which is below.
			The lower part perfoms standart function.
			The upper part functions as three buttons.
				
				Click on the left side will launch the skip animation of tonic combo for the tonic which is in the left slot.
				Click on the right side will launch the skip animation of tonic combo for the tonic which is in the right slot.
				Click on the center will launch the skip animation of tonic combo for the tonic which is in the center slot.
				
				When your character is in dead eye you need to quicly click on the necessary button to activate this combo. Or you can just hold down this button and the script will do it for you.
				
		Quickly put on a hat from a legendary costume
			
			Put on a costume made of a legendary animal before this and place the hat of this animal in the slot for 7 hours.
			The lower part of the touch panel is divided as follows 
				
				The right part (66 percent) performs standard functions, the left part (the remaining 33 percent) performs the function of automatically donning a hat from the lower left slot (hat from a legendary animal costume)
				
				You can hold this button like in the skip tonics.
				
		Unpredictable situations or bugs
			
			If something went wrong and you can not understand what exactly, you need to change the memory cell in сronus and return it back to the previous value to reset.
			Or write to us so that we can help solve your problem
				
				E9022#9022(Discord)
				
	Authors (Team MISSION WINNOW)
		
		Alexander Woropai
		Sonya Woropai
		Dmitry Mironenko
		
	Special thanks to Vlad and Mobin
________________________________________________________________________________________________________________________________________________________________________________________________________________








              %%%%    %%%%   %%    %%%%%     %%%%%%   %%%     %%%%%%    %%%      %%%                    
               %%%%%   %%%%%  %%  %%%       %%         %%%   %%%    %%%  %%%%%%    %%                   
                %%% %% %% %%   %%   %%%%%%%   %%%%%%%%   %%  %%%      %%% %%% %%%   %%                  
                 %%  %%%%  %%  %%%        %%%        %%% %%%  %%%     %%%  %%    %%%%%%                 
                  %%  %%%  %%%  %%% %%%%%%%%   %%%%%%%%   %%%   %%%%%%%%   %%%      %%%%                
                  %%%       %%%  %%                                         %%%        %%               
                 %%%  %%%  %%%  %%%  %%%%    %%%  %%%%    %%%  %%%%%%%%%%  %%%  %%%  %%%                
                 %%  %%%% %%%  %%%  %%%%%   %%%  %%%%%%  %%% %%%      %%%  %%  %%%% %%%                 
                %%%%%  %% %%  %%%  %%%  %% %%%  %%%  %%  %%  %%%      %%  %%%%%  %% %%                  
               %%%%%   %%%%%  %%  %%%    %%%%%  %%    %%%%   %%%%%%%%%%  %%%%%   %%%%%                  














                                                                                                                                                                                                                                                                                             */






define Green   = 0;
define White   = 1;
define Red     = 2;
define Off     = 3;
define Yellow  = 4;
define Pink    = 5;
define SkyBlue = 6;

int BATTERY;


data(

0,0,1,0, // Green
1,1,1,0, // White
0,1,0,0, // Red
0,0,0,0, // Off
0,1,1,0, // Yellow
0,0,0,1, // Pink
1,0,1,0  // SkyBlue

);



int REPEATER_OR_PISTOLS, SNIPER_RIFLE; 

int AIM_ASSIST_ON;

int AUTO_HEADSHOT_RUNNING;
int MENU_SECTION_RUNNING;
int AIM_ASSIST_RUNNING;
int SELECTOR_RUNNING;
int MIDDLE_RUNNING;
int LONG_RUNNING;

int SELECTOR_SECTION;
int R3_ACTIVATION;
int PLACE_ON_MENU;
int RX;
int RY;


int L1_PRESS;
int R2_PRESS;

int HEADSHOT_MODE;
int SNIPER_MODE;

int WAIT;
int TONICS_RX;
int TONICS_RY;
int TONICS;
int VIBRATE_RUMBLE;





main 

{

    vm_tctrl(-9);

/////////////////////////////////////////////////////////////////////   CHANGE_SETTINGS_OF_MODES

    if(!MENU_SECTION_RUNNING&&!AIM_ASSIST_RUNNING&&!AUTO_HEADSHOT_RUNNING&&get_lval(PS4_L1))
    {
    	SELECTOR_RUNNING=TRUE;
    	if(event_press(PS4_R1)) SELECTOR_SECTION=!SELECTOR_SECTION;
    	RX=get_lval(PS4_RX);
    	RY=get_lval(PS4_RY);
    }
    
    
    if(SELECTOR_RUNNING&&event_release(PS4_L1))
    {
    	SELECTOR_RUNNING=FALSE;
    	if(!SELECTOR_SECTION)
    	{
    		if((RX*RX+RY*RY)>=150)
    		{ 
    			RY=-1*RY;   
    			if(((100*RY)-(41*RX))>0)                                 {   REPEATER_OR_PISTOLS=TRUE;  SNIPER_RIFLE=FALSE; } 
    			else
    			{
    				if((((100*RY)-(241*RX))>0)||(((100*RY)+(241*RX))>0)) {   REPEATER_OR_PISTOLS=FALSE; SNIPER_RIFLE=FALSE; }
    				else                                                 {   REPEATER_OR_PISTOLS=FALSE; SNIPER_RIFLE=TRUE;  }
    			}    
    		}
    	}
    	SELECTOR_SECTION=FALSE;
    	RX=0;
    	RY=0;
    }
    
    if(get_lval(PS4_FINGER1Y)<-20||get_lval(PS4_FINGER1X)<=-66)
    {
    	set_val(PS4_TOUCH,0);
    	
    	if(get_lval(PS4_TOUCH))
    	{
    		if(get_lval(PS4_FINGER1Y)<-20)
    		{
    			if(get_lval(PS4_FINGER1X)<=-66)                           { TONICS_RX = -100; TONICS_RY = -100; TONICS=100; }
    			if(get_lval(PS4_FINGER1X)>-66&&get_lval(PS4_FINGER1X)<66) { TONICS_RX = 0;    TONICS_RY = -100; TONICS=100; }
    			if(get_lval(PS4_FINGER1X)>=66)                            { TONICS_RX = 100;  TONICS_RY = -100; TONICS=100; }
    		}
    		else if(get_lval(PS4_FINGER1X)<=-66)                          { TONICS_RX = -100; TONICS_RY = 100; TONICS=0; }
    		combo_run(TONICS);
    	}
    }
    
    
    if(get_battery()<=1) combo_run(LOW_BATTERY);
    
    if(!BATTERY)
    {   
    	if(MENU_SECTION_RUNNING)set_ds4_led(Yellow);
    	else
    	if(!SNIPER_RIFLE&&!REPEATER_OR_PISTOLS) set_ds4_led(White);
    	
    	if(SNIPER_RIFLE)
    	{
    		if(!SNIPER_MODE)set_ds4_led(White);
    		else
    		{
    			if(test_bit(SNIPER_MODE,0))set_ds4_led(Green);
    			else set_ds4_led(Red);
    		}
    	}
    	
    	if(REPEATER_OR_PISTOLS)
    	{
    		if(!HEADSHOT_MODE)set_ds4_led(White);
    		else
    		{
    			if(test_bit(HEADSHOT_MODE,0))set_ds4_led(SkyBlue);
    			else set_ds4_led(Pink);
    		}
    	}
    }
    
    deadzone(PS4_L2,PS4_R2,99,99);
     
/////////////////////////////////////////////////////////////////////   CHANGE_SETTINGS_OF_MODES



/////////////////////////////////////////////////////////////////////   MENU_SECTION
    
    if(MENU_SECTION_RUNNING)
    {
    	if(event_press(PS4_CROSS)) PLACE_ON_MENU = PLACE_ON_MENU + 1;  
    	if(event_press(PS4_CIRCLE))PLACE_ON_MENU = PLACE_ON_MENU - 1; 
    	if((PLACE_ON_MENU<0)||(get_lval(PS4_CIRCLE)&&get_ptime(PS4_CIRCLE)>800)){ SNIPER_RIFLE=TRUE; PLACE_ON_MENU=0; MENU_SECTION_RUNNING=FALSE;  } 
    }
    else
    if(get_lval(PS4_CROSS)&&get_ptime(PS4_CROSS)>2000)combo_run(RUN_HORSE); 
    
/////////////////////////////////////////////////////////////////////   MENU_SECTION



/////////////////////////////////////////////////////////////////////   SNIPER
    if(SNIPER_RIFLE&&!SELECTOR_RUNNING)
    {
    	if(get_lval(PS4_L2))
    	{
    		set_val(PS4_L1,0);
    		AIM_ASSIST_RUNNING=TRUE;
    		
    		if(event_release(PS4_RIGHT)) SNIPER_MODE = SNIPER_MODE + 1;
    		if(get_lval(PS4_RIGHT)&&get_ptime(PS4_RIGHT)>500) SNIPER_MODE = 0;
    		
    		if(!L1_PRESS&&get_lval(PS4_L1)){ L1_PRESS=TRUE; AIM_ASSIST_ON=!AIM_ASSIST_ON; vibrate(AIM_ASSIST_ON); } 
    		
    		if(AIM_ASSIST_ON)combo_run(AIM_ASSIST);
    		
    		if(SNIPER_MODE)
    		{
    			if(test_bit(SNIPER_MODE,0)&&!R2_PRESS&&get_lval(PS4_R2))             { R2_PRESS=TRUE; combo_run(RAPID_CARCANO); }
    			if(!test_bit(SNIPER_MODE,0)&&get_lval(PS4_R2)&&get_ptime(PS4_R2)>350){ set_val(PS4_R2,0); combo_run(HORSE_RAPID_CARCANO); }
    		}
    	}
    	
    	if(event_release(PS4_L2))AIM_ASSIST_RUNNING=FALSE;
    	
    	if(SNIPER_MODE)
    	{
    		if(!SELECTOR_RUNNING)
    		{
    			if(test_bit(SNIPER_MODE,0)||!get_lval(PS4_L2))set_val(PS4_R2,0);
    			if(!get_lval(PS4_L2)&&get_lval(PS4_R2)) combo_run(HIP_FIRE); 
    		}
    		
    		if((get_lval(PS4_LEFT)&&get_ptime(PS4_LEFT)>800)||(get_lval(PS4_RIGHT)&&get_ptime(PS4_RIGHT)>800)||event_press(PS4_OPTIONS))
    		{
    			MENU_SECTION_RUNNING=TRUE; 
    			SNIPER_RIFLE=FALSE;
    		}
    	}
    }
    
    if(R2_PRESS&&!get_lval(PS4_R2)) R2_PRESS=FALSE; 
    if(L1_PRESS&&!get_lval(PS4_L1)) L1_PRESS=FALSE;
/////////////////////////////////////////////////////////////////////   SNIPER



/////////////////////////////////////////////////////////////////////   AUTO_HEADSHOT
    if(REPEATER_OR_PISTOLS&&!SELECTOR_RUNNING)
    {
    	if(get_lval(PS4_L2))
    	{
    		if(event_release(PS4_RIGHT)) HEADSHOT_MODE = HEADSHOT_MODE + 1;
    		if(get_lval(PS4_RIGHT)&&get_ptime(PS4_RIGHT)>500) HEADSHOT_MODE = 0;
    		
    		AUTO_HEADSHOT_RUNNING=TRUE;
    		
    		set_val(PS4_L1,0);
    		
    		if(!HEADSHOT_MODE)
    		{
    			if(event_press(PS4_R3))R3_ACTIVATION = !R3_ACTIVATION;
    			if(!LONG_RUNNING&&get_lval(PS4_L1)){ WAIT=100; combo_run(LONG_DISTANCE); if(!R3_ACTIVATION) combo_run(R3_PRESS); }
    			
    			if(LONG_RUNNING)
    			{
    				if(!MIDDLE_RUNNING&&get_lval(PS4_L1))combo_run(PLUS_DISTANCE);
    				else MIDDLE_RUNNING=TRUE; 
    			}
    		}
    		
    		if(HEADSHOT_MODE&&!LONG_RUNNING&&!MIDDLE_RUNNING)
    		{
    			if(!LONG_RUNNING&&test_bit(HEADSHOT_MODE,0))   {  WAIT=250; combo_run(LONG_DISTANCE);   }  
    			if(!MIDDLE_RUNNING&&!test_bit(HEADSHOT_MODE,0)){  WAIT=250; combo_run(MIDDLE_DISTANCE); }
    			combo_run(R3_PRESS); 
    		}
    		
    	}
    	
    	if(event_release(PS4_L2)){ combo_stop(R3_PRESS); combo_stop(LONG_DISTANCE); combo_stop(PLUS_DISTANCE); combo_stop(MIDDLE_DISTANCE); MIDDLE_RUNNING=FALSE; LONG_RUNNING=FALSE; R3_ACTIVATION=FALSE; AUTO_HEADSHOT_RUNNING=FALSE; }
    	
    	if(get_lval(PS4_R2)&&get_ptime(PS4_R2)>130)combo_run(R2_PRESS);
    }
/////////////////////////////////////////////////////////////////////   AUTO_HEADSHOT



}



combo LOW_BATTERY{
BATTERY=TRUE;
set_ds4_led(Off);
wait(500);
BATTERY=FALSE;
wait(1500);
}

combo RUN_HORSE
{
	set_val(PS4_CROSS,0);
	wait(50);
	wait(100);
}

combo R2_PRESS
{
	set_val(PS4_R2,0);
	wait(50);
	wait(50);
}

combo HORSE_RAPID_CARCANO
{
	set_val(PS4_R2,100);
	set_val(PS4_TOUCH,100);
	wait(50);
	wait(100);
}

combo R3_PRESS
{
	wait(WAIT); 
	set_val(PS4_R3,100);
	wait(100);
	wait(110);
}

combo HIP_FIRE
{
	set_val(PS4_L2,100);
	wait(20);
	set_val(PS4_L2,100);
	set_val(PS4_R2,100);
	wait(40);
	wait(400);
	set_val(PS4_L2,100);
	wait(70);
	wait(100);
}

combo TONICS
{
	set_val(PS4_L1,100);
	set_val(PS4_R1,100);
	wait(60); 
	set_val(PS4_RX, TONICS_RX);
	set_val(PS4_RY, TONICS_RY);
	set_val(PS4_SQUARE,TONICS);
	wait(80); 
	wait(100);
}

combo RAPID_CARCANO
{
	set_val(PS4_R2,100);
	set_val(PS4_CROSS,100);
	wait(40);
	wait(180);
	set_val(PS4_R2,100);
	set_val(PS4_CROSS,100);
	wait(40);
	wait(50);
}

combo LONG_DISTANCE
{
	wait(WAIT);
	set_val(PS4_RY, -100);
	set_val(PS4_RX, -30);
	wait(140);
	LONG_RUNNING=TRUE;
}

combo PLUS_DISTANCE
{
	set_val(PS4_RY, -100);
	set_val(PS4_RX, -15);
	wait(40); 
	MIDDLE_RUNNING=TRUE;
}

combo MIDDLE_DISTANCE
{
	call(LONG_DISTANCE);
	call(PLUS_DISTANCE);
}

combo AIM_ASSIST
{
	set_val(PS4_RX, XY(PS4_RX, 20)); 
	wait(40); 
	wait(40);
	set_val(PS4_RX, XY(PS4_RX, -20)); 
	wait(40);
	wait(40);
	set_val(PS4_RY, XY(PS4_RY, 20)); 
	wait(40); 
	wait(40);
	set_val(PS4_RY, XY(PS4_RY, -20)); 
	wait(40); 
	wait(40);
}

combo vibrate
{
	set_rumble(VIBRATE_RUMBLE,100);
	wait(200);
	reset_rumble();
}

function XY(BUTTON, VALUE)
{
	if(abs(get_lval(BUTTON))<25)
	return VALUE;
	return get_lval(BUTTON);
}

function set_ds4_led(colour) 
{
	set_led(LED_1, duint8(colour * 4));
	set_led(LED_2, duint8((colour * 4) + 1));
	set_led(LED_3, duint8((colour * 4) + 2));
	set_led(LED_4, duint8((colour * 4) + 3)); 
}

function vibrate(BOOL)
{
	if(BOOL) VIBRATE_RUMBLE = RUMBLE_A; 
	if(!BOOL)VIBRATE_RUMBLE = RUMBLE_B;
	combo_run(vibrate);
}